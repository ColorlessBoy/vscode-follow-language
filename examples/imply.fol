/* 
# imply 
*/

thm id(prop p1) {
  |- imp(p1, p1)
} = {
  ax.mp(imp(p1, p1), imp(p1, imp(p1, p1)))
  ax.1(p1, p1)
  ax.mp(imp(imp(p1, imp(p1, p1)), imp(p1, p1)), imp(p1, imp(imp(p1, p1), p1)))
  ax.2(p1, imp(p1, p1), p1)
  ax.1(p1, imp(p1, p1))
}

thm a1i(prop p1, prop p2) {
  |- imp(p1, p2)
  -| p2
} = {
  ax.mp(imp(p1,p2), p2)
  ax.1(p2, p1)
}


thm idd(prop p0, prop p1) {
  |- imp(p0, imp(p1, p1))
} = {
  a1i(p0, imp(p1,p1))
  id(p1)
}

thm mpd(prop p1, prop p2, prop p3) {
  |- imp(p1, p2)
  -| imp(p1, p3)
  -| imp(p1, imp(p3, p2))
} = {
  ax.mp(imp(p1, p2), imp(p1, p3))
  ax.mp(imp(imp(p1, p3), imp(p1, p2)), imp(p1, imp(p3, p2)))
  ax.2(p1, p3, p2)
}

thm syl(prop p1, prop p2, prop p3) {
  |- imp(p1, p2)
  -| imp(p1, p3)
  -| imp(p3, p2)
} = {
  mpd(p1, p2, p3)
  a1i(p1, imp(p3,p2))
}

thm a2i(prop p1, prop p2, prop p3) {
  |- imp(imp(p1, p2), imp(p1, p3))
  -| imp(p1, imp(p2, p3))
} = {
  ax.mp(imp(imp(p1,p2),imp(p1,p3)), imp(p1,imp(p2,p3)))
  ax.2(p1, p2, p3)
}

thm a2ii(prop p1, prop p2, prop p3) {
  |- imp(p1, p2)
  -| imp(p1, p3)
  -| imp(p1, imp(p3, p2))
} = {
  ax.mp(imp(p1,p2), imp(p1, p3))
  a2i(p1, p3, p2)
}

thm a1d(prop p1, prop p2, prop p3) {
  |- imp(p1, imp(p2, p3))
  -| imp(p1, p3)
} = {
  syl(p1, imp(p2,p3), p3)
  ax.1(p3, p2)
}

thm a2d(prop p1, prop p2, prop p3, prop p4) {
  |- imp(p1, imp(imp(p2, p3), imp(p2, p4)))
  -| imp(p1, imp(p2, imp(p3, p4)))
} = {
  syl(p1, imp(imp(p2,p3),imp(p2,p4)), imp(p2, imp(p3, p4)))
  ax.2(p2, p3, p4)
}

thm a2.3(prop p1, prop p2, prop p3, prop p4) {
  |- imp(imp(p1,p2), imp(imp(p1,p3), imp(p1,p4)))
  -| imp(p1, imp(p2, imp(p3, p4)))
} = {
  a2d(imp(p1,p2), p1, p3, p4)
  a2i(p1, p2, imp(p3,p4))
}

thm a2id(prop p1, prop p2, prop p3, prop p4) {
  |- imp(p1, imp(p2, p3))
  -| imp(p1, imp(p2, p4))
  -| imp(p1, imp(p2, imp(p4, p3)))
} = {
  mpd(p1, imp(p2,p3), imp(p2,p4))
  a2d(p1, p2, p4, p3)
}

thm com12(prop p1, prop p2, prop p3) {
  |- imp(p1, imp(p2, p3))
  -| imp(p2, imp(p1, p3))
} = {
  syl(p1, imp(p2,p3), imp(p2,p1))
  ax.1(p1, p2)
  a2i(p2, p1, p3)
}
thm com12.d(prop p1, prop p2, prop p3) {
  |- imp(imp(p1, imp(p2, p3)), imp(p2, imp(p1, p3)))
} = {
  com12(imp(p1,imp(p2,p3)), p2, imp(p1,p3))
  a2d(p2, p1, imp(p2,p3), p3)
  com12(p2, p1, imp(imp(p2,p3),p3))
  a1i(p1, imp(p2,imp(imp(p2,p3),p3)))
  com12(p2, imp(p2,p3), p3)
  id(imp(p2,p3))
}

thm imp.trans.2(prop p1, prop p2, prop p3) {
  |- imp(imp(p2, p3), imp(imp(p1, p2), imp(p1, p3)))
} = {
  a2d(imp(p2,p3), p1, p2, p3)
  ax.1(imp(p2,p3), p1)
}

thm imp.trans.1(prop p1, prop p2, prop p3) {
  |- imp(imp(p1, p2), imp(imp(p2, p3), imp(p1, p3)))
} = {
  com12(imp(p1,p2), imp(p2,p3), imp(p1,p3))
  imp.trans.2(p1, p2, p3)
}

thm syl.d.1(prop p1, prop p2, prop p3) {
  |- imp(imp(p1, p2), imp(p1, p3))
  -| imp(p2, p3)
} = {
  a2i(p1, p2, p3)
  a1i(p1, imp(p2,p3))
}

thm syl.d.1.d(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, imp(imp(p1, p2), imp(p1, p3)))
  -| imp(p0,imp(p2, p3))
} = {
  syl(p0, imp(imp(p1,p2),imp(p1,p3)), imp(p2, p3))
  imp.trans.2(p1, p2, p3)
}

thm syl.d.2(prop p1, prop p2, prop p3) {
  |- imp(imp(p1, p3), imp(p2, p3))
  -| imp(p2, p1)
} = {
  com12(imp(p1,p3), p2, p3)
  syl(p2, imp(imp(p1,p3),p3), p1)
  com12(p1, imp(p1,p3), p3)
  id(imp(p1,p3))
}

thm syl.d.2.d(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, imp(imp(p1, p3), imp(p2, p3)))
  -| imp(p0, imp(p2, p1))
} = {
  syl(p0, imp(imp(p1,p3),imp(p2,p3)), imp(p2,p1))
  imp.trans.1(p2, p1, p3)
}

thm syld(prop p1, prop p2, prop p3, prop p4) {
  |- imp(p1, imp(p2, p3))
  -| imp(p1, imp(p2, p4))
  -| imp(p1, imp(p4, p3))
} = {
  mpd(p1, imp(p2,p3), imp(p2,p4))
  a2d(p1, p2, p4, p3)
  a1d(p1, p2, imp(p4,p3))
}

thm syl5(prop p1, prop p2, prop p3, prop p4) {
  |- imp(p1, imp(p2, p3))
  -| imp(p1, imp(p4, p3))
  -| imp(p2, p4)
} = {
  syld(p1, p2, p3, p4)
  a1i(p1, imp(p2,p4))
}

thm syl6(prop p1, prop p2, prop p3, prop p4) {
  |- imp(p1, imp(p2, p3))
  -| imp(p1, imp(p2, p4))
  -| imp(p4, p3)
} = {
  syld(p1, p2, p3, p4)
  a1i(p1, imp(p4,p3))
}

thm syladd(prop p1, prop p2, prop p3, prop p4) {
  |- imp(p1, p2)
  -| imp(p1, p3)
  -| imp(p1, p4)
  -| imp(p3, imp(p4, p2))
} = {
  a2ii(p1, p2, p4)
  syl(p1, imp(p4,p2), p3)
}

thm ax2r.i(prop p1, prop p2, prop p3) {
  |- imp(p1, imp(p2, p3))
  -| imp(imp(p1, p2), imp(p1, p3))
} = {
  ax.mp(imp(p1,imp(p2,p3)), imp(p1, imp(imp(p1,p2),p3)))
  com12(p1, imp(p1,p2), p3)
  a2i(p1, imp(imp(p1,p2),p3), imp(p2,p3))
  syl.d.2.d(p1, imp(p1,p2), p2, p3)
  a1i(p1, imp(p2,imp(p1,p2)))
  ax.1(p2, p1)
}

thm syl6d(prop p1, prop p2, prop p3, prop p4, prop p5) {
  |- imp(p1, imp(p2, imp(p3, p4)))
  -| imp(p1, imp(p2, imp(p3, p5)))
  -| imp(p1, imp(p5, p4))
} = {
  ax.mp(imp(p1,imp(p2,imp(p3,p4))), imp(p1, imp(p2, imp(p3, p5))))
  a2i(p1, imp(p2,imp(p3,p5)), imp(p2,imp(p3,p4)))
  syl(p1, imp(imp(p2,imp(p3,p5)),imp(p2,imp(p3,p4))), imp(p2, imp(imp(p3,p5),imp(p3,p4))))
  ax.2(p2, imp(p3,p5), imp(p3,p4))
  syl6(p1, p2, imp(imp(p3,p5),imp(p3,p4)), imp(p3,imp(p5,p4)))
  ax.2(p3, p5, p4)
  syl(p1, imp(p2,imp(p3,imp(p5,p4))), imp(p5,p4))
  com12(imp(p5,p4), p2, imp(p3,imp(p5,p4)))
  a1i(p2, imp(imp(p5,p4),imp(p3,imp(p5,p4))))
  ax.1(imp(p5,p4), p3)
}

thm imp.trans(prop p0, prop p1, prop p2) {
  |- imp(imp(p0,p1), imp(imp(p2,p0), imp(p2,p1)))
  |- imp(imp(p0,p1), imp(imp(p1,p2), imp(p0,p2)))
} = {
  imp.trans.1(p0, p1, p2)
  imp.trans.2(p2, p0, p1)
}

thm com23(prop p1, prop p2, prop p3, prop p4) {
  |- imp(p1, imp(p2, imp(p3,p4)))
  -| imp(p1, imp(p3, imp(p2, p4)))
} = {
  syl(p1, imp(p2,imp(p3,p4)), imp(p3,imp(p2,p4)))
  com12.d(p3, p2, p4)
}

thm imp.imp.imp(prop p1, prop p2, prop p3, prop p4) {
  |- imp(imp(p1,p2), imp(p3,p4))
  -| imp(p3,p1)
  -| imp(p2,p4)
} = {
  ax.mp(imp(imp(p1,p2),imp(p3,p4)), imp(p3,p1))
  syl5(imp(p3,p1), imp(p1,p2), imp(p3,p4), imp(p1,p4))
  imp.trans(p3, p1, p4)
  ax.mp(imp(imp(p1,p2),imp(p1,p4)), imp(p2,p4))
  imp.trans(p2, p4, p1)
}

thm imp.imp.imp.imp(prop p0, prop p1, prop p2, prop p3, prop p4) {
    |- imp(p0, imp(imp(p1,p2), imp(p3, p4)))
    -| imp(p0, imp(p3, p1))
    -| imp(p0, imp(p2, p4))
} = {
  a2ii(p0, imp(imp(p1,p2),imp(p3,p4)), imp(p3,p1))
  syl(p0, imp(imp(p3,p1),imp(imp(p1,p2),imp(p3,p4))), imp(p2,p4))
  com23(imp(p2,p4), imp(p3,p1), imp(p1,p2), imp(p3,p4))
  syl6(imp(p2,p4), imp(p1,p2), imp(imp(p3,p1),imp(p3,p4)), imp(p1,p4))
  imp.trans(p1, p4, p3)
  imp.trans(p2, p4, p1)
}

thm syld2(prop p1, prop p2, prop p3, prop p4, prop p5) {
  |- imp(p1, imp(p2, p3))
  -| imp(p1, imp(p4, p5))
  -| imp(p1, imp(p2, p4))
  -| imp(p1, imp(p5, p3))
} = {
  syld(p1, p2, p3, p4)
  syld(p1, p4, p3, p5)
}

thm syl56(prop p1, prop p2, prop p3, prop p4, prop p5) {
  |- imp(p1, imp(p2, p3))
  -| imp(p1, imp(p4, p5))
  -| imp(p2, p4)
  -| imp(p5, p3)
} = {
  syld2(p1, p2, p3, p4, p5)
  a1i(p1, imp(p2,p4))
  a1i(p1, imp(p5,p3))
}