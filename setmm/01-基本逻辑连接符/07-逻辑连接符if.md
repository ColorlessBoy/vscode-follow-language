# 逻辑连接符 `if` 

```follow
term prop if(prop p0, prop p1, prop p2) { if ( p0, p1, p2 ) }
axiom if.def.1(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), or(and(p0, p1), and(not(p0), p2)))
}
```


## 等价定义2

```follow
thm if.def.2.lemma1(prop p0, prop p1, prop p2) {
  |- imp(or(and(p0, p1), and(not(p0), p2)), and(imp(p0, p1), imp(not(p0), p2)))
} = {
  or.casesii(and(p0,p1), and(not(p0),p2), and(imp(p0,p1),imp(not(p0),p2)))
  and.introd(and(p0,p1), imp(p0,p1), imp(not(p0),p2))
  and.impoi(p0, p1, imp(p0,p1))
  a1i(p0, imp(p1,imp(p0,p1)))
  a1(p1, p0)
  syl(and(p0,p1), imp(not(p0),p2), p0)
  and.left(p0, p1)
  cont(p0, p2)
  and.introd(and(not(p0),p2), imp(p0,p1), imp(not(p0),p2))
  syl(and(not(p0),p2), imp(p0,p1), not(p0))
  and.left(not(p0), p2)
  cont(p0, p1)
  and.impoi(not(p0), p2, imp(not(p0),p2))
  a1i(not(p0), imp(p2,imp(not(p0),p2)))
  a1(p2, not(p0))
}
```

```follow
thm if.def.2.lemma2(prop p0, prop p1, prop p2) {
  |- imp(and(imp(p0, p1), imp(not(p0), p2)), or(and(p0, p1), and(not(p0), p2)))
} = {
  syl(and(imp(p0,p1),imp(not(p0),p2)), or(and(p0,p1),and(not(p0),p2)), imp(not(and(p0,p1)),and(not(p0),p2)))
  or.def(and(p0,p1), and(not(p0),p2))
  rw3(and(imp(p0,p1),imp(not(p0),p2)), not(and(p0,p1)), and(not(p0),p2), not(imp(not(p0),not(p2))))
  and.def(not(p0), p2)
  con3d(and(imp(p0,p1),imp(not(p0),p2)), and(p0,p1), imp(not(p0),not(p2)))
  rw3(and(imp(p0,p1),imp(not(p0),p2)), imp(not(p0),not(p2)), and(p0,p1), not(imp(p0,not(p1))))
  and.def(p0, p1)
  and.impoi(imp(p0,p1), imp(not(p0),p2), imp(imp(not(p0),not(p2)),not(imp(p0,not(p1)))))
  com12i(imp(p0,p1), imp(not(p0),p2), imp(imp(not(p0),not(p2)),not(imp(p0,not(p1)))))
  com23i(imp(not(p0),p2), imp(p0,p1), imp(not(p0),not(p2)), not(imp(p0,not(p1))))
  rw3(imp(not(p0),p2), imp(not(p0),not(p2)), imp(imp(p0,p1),not(imp(p0,not(p1)))), p0)
  cont(p0, p2)
  com12i(p0, imp(p0,p1), not(imp(p0,not(p1))))
  con1d(imp(p0,p1), p0, imp(p0,not(p1)))
  cont(p0, p1)
}
```

```follow
thm if.def.2.lemma3(prop p0, prop p1, prop p2) {
  |- imp(or(and(p0, p1), and(not(p0), p2)), and(imp(p0, p1), imp(not(p0), p2)))
  |- imp(and(imp(p0, p1), imp(not(p0), p2)), or(and(p0, p1), and(not(p0), p2)))
  |- iff(or(and(p0, p1), and(not(p0), p2)), and(imp(p0, p1), imp(not(p0), p2)))
  |- iff(and(imp(p0, p1), imp(not(p0), p2)), or(and(p0, p1), and(not(p0), p2)))
} = {
  iff.introii(or(and(p0,p1),and(not(p0),p2)), and(imp(p0,p1),imp(not(p0),p2)))
  iff.introii(and(imp(p0,p1),imp(not(p0),p2)), or(and(p0,p1),and(not(p0),p2)))
  if.def.2.lemma1(p0, p1, p2)
  if.def.2.lemma2(p0, p1, p2)
}
```

```follow
thm if.def.2(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), and(imp(p0, p1), imp(not(p0), p2)))
} = {
  iff.syl(if(p0,p1,p2), and(imp(p0,p1),imp(not(p0),p2)), or(and(p0,p1),and(not(p0),p2)))
  if.def.1(p0, p1, p2)
  if.def.2.lemma3(p0, p1, p2)
}
```

## 等价定义3

```follow
thm if.def.3.lemma3(prop p0, prop p1, prop p2) {
  |- iff(or(and(p0, p1), and(not(p0), p2)), and(imp(p0, p1), or(p0, p2)))
} = {
  iff.syl(or(and(p0,p1),and(not(p0),p2)), and(imp(p0,p1),or(p0,p2)), and(imp(p0,p1),imp(not(p0),p2)))
  if.def.2.lemma3(p0, p1, p2)
  and.iffandii(imp(p0,p1), imp(not(p0),p2), imp(p0,p1), or(p0,p2))
  iff.id(imp(p0,p1))
  iff.introii(imp(not(p0),p2), or(p0,p2))
  or.def(p0, p2)
}
```

```follow
thm if.def.3(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), and(imp(p0, p1), or(p0, p2)))
} = {
  iff.syl(if(p0,p1,p2), and(imp(p0,p1),or(p0,p2)), or(and(p0,p1),and(not(p0),p2)))
  if.def.3.lemma3(p0, p1, p2)
  if.def.1(p0, p1, p2)
}
```

## 等价定义4

```follow
thm if.def.4.lemma3(prop p0, prop p1, prop p2) {
  |- iff(or(and(p0, p1), and(not(p0), p2)), and(or(not(p0), p1), or(p0, p2)))
} = {
  iff.syl(or(and(p0,p1),and(not(p0),p2)), and(or(not(p0),p1),or(p0,p2)), and(imp(p0,p1),or(p0,p2)))
  if.def.3.lemma3(p0, p1, p2)
  and.iffandii(imp(p0,p1), or(p0,p2), or(not(p0),p1), or(p0,p2))
  iff.id(or(p0,p2))
  iff.syl(imp(p0,p1), or(not(p0),p1), imp(not(not(p0)),p1))
  iff.or.def(not(p0), p1)
  imp.iffimpii(p0, not(not(p0)), p1, p1)
  iff.id(p1)
  iff.notnot(p0)
}
```

```follow
thm if.def.4(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), and(or(not(p0), p1), or(p0, p2)))
} = {
  iff.syl(if(p0,p1,p2), and(or(not(p0),p1),or(p0,p2)), or(and(p0,p1),and(not(p0),p2)))
  if.def.4.lemma3(p0, p1, p2)
  if.def.1(p0, p1, p2)
}
```

## 等价定义5 

```follow
thm if.def.5.lemma(prop p0, prop p1, prop p2) {
  |- iff(or(and(p0, p1), and(not(p0), p2)), and(or(not(p0), p1), imp(not(p0), p2)))
} = {
  iff.syl(or(and(p0,p1),and(not(p0),p2)), and(or(not(p0),p1),imp(not(p0),p2)), and(or(not(p0),p1),or(p0,p2)))
  if.def.4.lemma3(p0, p1, p2)
  and.iffandii(or(not(p0),p1), or(p0,p2), or(not(p0),p1), imp(not(p0),p2))
  iff.id(or(not(p0),p1))
  iff.or.def(p0, p2)
}
```

```follow
thm if.def.5(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), and(or(not(p0), p1), imp(not(p0), p2)))
} = {
  iff.syl(if(p0,p1,p2), and(or(not(p0),p1),imp(not(p0),p2)), or(and(p0,p1),and(not(p0),p2)))
  if.def.5.lemma(p0, p1, p2)
  if.def.1(p0, p1, p2)
}
```

## 等价定义6

```follow
thm if.def.6.lemma(prop p0, prop p1, prop p2) {
  |- iff(or(and(p0, p1), and(not(p0), p2)), or(and(p0, p1), not(imp(p2, p0))))
} = {
  or.ifforii(and(p0,p1), and(p0,p1), and(not(p0),p2), not(imp(p2,p0)))
  iff.id(and(p0,p1))
  iff.syl(and(not(p0),p2), not(imp(p2,p0)), not(imp(not(p0),not(p2))))
  iff.and.def(not(p0), p2)
  mp(iff(not(imp(not(p0),not(p2))),not(imp(p2,p0))), iff(imp(p2,p0),imp(not(p0),not(p2))))
  iff.con(imp(p2,p0), imp(not(p0),not(p2)))
  iff.con(p2, p0)
}
```

```follow
thm if.def.6(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), or(and(p0, p1), not(imp(p2, p0))))
} = {
  iff.syl(if(p0,p1,p2), or(and(p0,p1),not(imp(p2,p0))), or(and(p0,p1),and(not(p0),p2)))
  if.def.6.lemma(p0, p1, p2)
  if.def.1(p0, p1, p2)
}
```


## 等价定义7

```follow
thm if.def.7.lemma(prop p0, prop p1, prop p2) {
  |- iff(or(and(p0, p1), and(not(p0), p2)), imp(imp(p2, p0), and(p0, p1)))
} = {
  iff.syl(or(and(p0,p1),and(not(p0),p2)), imp(imp(p2,p0),and(p0,p1)), or(and(p0,p1),not(imp(p2,p0))))
  if.def.6.lemma(p0, p1, p2)
  iff.syl(or(and(p0,p1),not(imp(p2,p0))), imp(imp(p2,p0),and(p0,p1)), imp(not(and(p0,p1)),not(imp(p2,p0))))
  iff.or.def(and(p0,p1), not(imp(p2,p0)))
  iff.con(and(p0,p1), imp(p2,p0))
}
```

```follow
thm if.def.7(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), imp(imp(p2, p0), and(p0, p1)))
} = {
  iff.syl(if(p0,p1,p2), imp(imp(p2,p0),and(p0,p1)), or(and(p0,p1),and(not(p0),p2)))
  if.def.7.lemma(p0, p1, p2)
  if.def.1(p0, p1, p2)
}
```

## 定义汇总

```follow
thm if.def(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), or(and(p0, p1), and(not(p0), p2)))
  |- iff(if(p0, p1, p2), and(imp(p0, p1), imp(not(p0), p2)))
  |- iff(if(p0, p1, p2), and(imp(p0, p1), or(p0, p2)))
  |- iff(if(p0, p1, p2), and(or(not(p0), p1), or(p0, p2)))
  |- iff(if(p0, p1, p2), and(or(not(p0), p1), imp(not(p0), p2)))
  |- iff(if(p0, p1, p2), or(and(p0, p1), not(imp(p2, p0))))
  |- iff(if(p0, p1, p2), imp(imp(p2, p0), and(p0, p1)))

  |- iff(or(and(p0, p1), and(not(p0), p2)), if(p0, p1, p2))
  |- iff(and(imp(p0, p1), imp(not(p0), p2)), if(p0, p1, p2))
  |- iff(and(imp(p0, p1), or(p0, p2)), if(p0, p1, p2))
  |- iff(and(or(not(p0), p1), or(p0, p2)), if(p0, p1, p2))
  |- iff(and(or(not(p0), p1), imp(not(p0), p2)), if(p0, p1, p2))
  |- iff(or(and(p0, p1), not(imp(p2, p0))), if(p0, p1, p2))
  |- iff(imp(imp(p2, p0), and(p0, p1)), if(p0, p1, p2))

  |- imp(if(p0, p1, p2), or(and(p0, p1), and(not(p0), p2)))
  |- imp(if(p0, p1, p2), and(imp(p0, p1), imp(not(p0), p2)))
  |- imp(if(p0, p1, p2), and(imp(p0, p1), or(p0, p2)))
  |- imp(if(p0, p1, p2), and(or(not(p0), p1), or(p0, p2)))
  |- imp(if(p0, p1, p2), and(or(not(p0), p1), imp(not(p0), p2)))
  |- imp(if(p0, p1, p2), or(and(p0, p1), not(imp(p2, p0))))
  |- imp(if(p0, p1, p2), imp(imp(p2, p0), and(p0, p1)))

  |- imp(or(and(p0, p1), and(not(p0), p2)), if(p0, p1, p2))
  |- imp(and(imp(p0, p1), imp(not(p0), p2)), if(p0, p1, p2))
  |- imp(and(imp(p0, p1), or(p0, p2)), if(p0, p1, p2))
  |- imp(and(or(not(p0), p1), or(p0, p2)), if(p0, p1, p2))
  |- imp(and(or(not(p0), p1), imp(not(p0), p2)), if(p0, p1, p2))
  |- imp(or(and(p0, p1), not(imp(p2, p0))), if(p0, p1, p2))
  |- imp(imp(imp(p2, p0), and(p0, p1)), if(p0, p1, p2))
} = {
  iff.lefti(if(p0,p1,p2), or(and(p0,p1),and(not(p0),p2)))
  iff.lefti(if(p0,p1,p2), and(imp(p0,p1),imp(not(p0),p2)))
  iff.lefti(if(p0,p1,p2), and(imp(p0,p1),or(p0,p2)))
  iff.lefti(if(p0,p1,p2), and(or(not(p0),p1),or(p0,p2)))
  iff.lefti(if(p0,p1,p2), and(or(not(p0),p1),imp(not(p0),p2)))
  iff.lefti(if(p0,p1,p2), or(and(p0,p1),not(imp(p2,p0))))
  iff.lefti(if(p0,p1,p2), imp(imp(p2,p0),and(p0,p1)))

  iff.righti(if(p0,p1,p2), or(and(p0,p1),and(not(p0),p2)))
  iff.righti(if(p0,p1,p2), and(imp(p0,p1),imp(not(p0),p2)))
  iff.righti(if(p0,p1,p2), and(imp(p0,p1),or(p0,p2)))
  iff.righti(if(p0,p1,p2), and(or(not(p0),p1),or(p0,p2)))
  iff.righti(if(p0,p1,p2), and(or(not(p0),p1),imp(not(p0),p2)))
  iff.righti(if(p0,p1,p2), or(and(p0,p1),not(imp(p2,p0))))
  iff.righti(if(p0,p1,p2), imp(imp(p2,p0),and(p0,p1)))

  iff.comi(or(and(p0,p1),and(not(p0),p2)), if(p0,p1,p2))
  iff.comi(imp(imp(p2,p0),and(p0,p1)), if(p0,p1,p2))
  iff.comi(or(and(p0,p1),not(imp(p2,p0))), if(p0,p1,p2))
  iff.comi(and(or(not(p0),p1),imp(not(p0),p2)), if(p0,p1,p2))
  iff.comi(and(or(not(p0),p1),or(p0,p2)), if(p0,p1,p2))
  iff.comi(and(imp(p0,p1),or(p0,p2)), if(p0,p1,p2))
  iff.comi(and(imp(p0,p1),imp(not(p0),p2)), if(p0,p1,p2))

  if.def.1(p0, p1, p2)
  if.def.2(p0, p1, p2)
  if.def.3(p0, p1, p2)
  if.def.4(p0, p1, p2)
  if.def.5(p0, p1, p2)
  if.def.6(p0, p1, p2)
  if.def.7(p0, p1, p2)
}
```

## 性质

```follow
thm and.2if(prop p0, prop p1, prop p2) {
  |- imp(and(p0, p1), if(p2, p0, p1))
} = {
  syl(and(p0,p1), if(p2,p0,p1), and(imp(p2,p0),imp(not(p2),p1)))
  if.def(p2, p0, p1)
  and.2andii(p0, imp(p2,p0), p1, imp(not(p2),p1))
  a1(p0, p2)
  a1(p1, not(p2))
}
```
```follow
thm if.2or(prop p0, prop p1, prop p2) {
  |- imp(if(p0, p1, p2), or(p1, p2))
} = {
  syl(if(p0,p1,p2), or(p1,p2), or(and(p0,p1),and(not(p0),p2)))
  if.def(p0, p1, p2)
  or.2orii(and(p0,p1), and(not(p0),p2), p1, p2)
  and.right(p0, p1)
  and.right(not(p0), p2)
}
```

```follow
thm if.not(prop p0, prop p1, prop p2) {
  |- iff(if(p0, p1, p2), if(not(p0), p2, p1))
  |- iff(if(not(p0), p2, p1), if(p0, p1, p2))
} = {
  iff.comi(if(not(p0),p2,p1), if(p0,p1,p2))
  iff.syl(if(p0,p1,p2), if(not(p0),p2,p1), or(and(p0,p1),and(not(p0),p2)))
  if.def(p0, p1, p2)
  iff.syl(or(and(p0,p1),and(not(p0),p2)), if(not(p0),p2,p1), or(and(not(p0),p2),and(not(not(p0)),p1)))
  if.def(not(p0), p2, p1)
  iff.syl(or(and(p0,p1),and(not(p0),p2)), or(and(not(p0),p2),and(not(not(p0)),p1)), or(and(not(p0),p2),and(p0,p1)))
  iff.or.com(and(p0,p1), and(not(p0),p2))
  or.ifforii(and(not(p0),p2), and(not(p0),p2), and(p0,p1), and(not(not(p0)),p1))
  iff.id(and(not(p0),p2))
  and.iffandii(p0, p1, not(not(p0)), p1)
  iff.notnot(p0)
  iff.id(p1)
}
```

```follow
thm if.true(prop p0, prop p1, prop p2) {
  |- imp(p0, iff(if(p0, p1, p2), p1))
  |- imp(p0, iff(p1, if(p0, p1, p2)))
} = {
  iff.comd(p0, p1, if(p0,p1,p2))
  iff.rw2(p0, if(p0,p1,p2), p1, and(imp(p0,p1),or(p0,p2)))
  if.def(p0, p1, p2)
  iff.introd(p0, and(imp(p0,p1),or(p0,p2)), p1)
  rw2(p0, and(imp(p0,p1),or(p0,p2)), p1, imp(p0,p1))
  and.left(imp(p0,p1), or(p0,p2))
  iidd(p0, p1)
  and.expoi(p0, p1, and(imp(p0,p1),or(p0,p2)))
  and.introd(and(p0,p1), imp(p0,p1), or(p0,p2))
  syl(and(p0,p1), imp(p0,p1), p1)
  and.right(p0, p1)
  a1(p1, p0)
  syl(and(p0,p1), or(p0,p2), p0)
  and.left(p0, p1)
  or.left(p0, p2)
}
```

```follow
thm if.false(prop p0, prop p1, prop p2) {
  |- imp(not(p0), iff(if(p0, p1, p2), p2))
  |- imp(not(p0), iff(p2, if(p0, p1, p2)))
} = {
  iff.comd(not(p0), p2, if(p0,p1,p2))
  iff.rw2(not(p0), if(p0,p1,p2), p2, if(not(p0),p2,p1))
  if.not(p0, p1, p2)
  if.true(not(p0), p2, p1)
}
```

```follow
thm if.id(prop p0, prop p1) {
  |- iff(if(p0, p1, p1), p1)
} = {
  cont4ii(p0, iff(if(p0,p1,p1),p1))
  if.true(p0, p1, p1)
  if.false(p0, p1, p1)
}
```

```follow
thm if.cases(prop p0, prop p1, prop p2, prop p3) {
  |- iff(p0, if(p1, p2, p3))
  -| imp(p1, iff(p0, p2))
  -| imp(not(p1), iff(p0, p3))
} = {
  cont4ii(p1, iff(p0,if(p1,p2,p3)))
  iff.syld(p1, p0, if(p1,p2,p3), p2)
  if.true(p1, p2, p3)
  iff.syld(not(p1), p0, if(p1,p2,p3), p3)
  if.false(p1, p2, p3)
}
```