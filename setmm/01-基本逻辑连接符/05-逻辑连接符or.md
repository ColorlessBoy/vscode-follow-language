# 逻辑连接符 `or` 

```follow
term prop or(prop p0, prop p1) { (p0 ∨ p1) }
axiom or.def(prop p0, prop p1) {
  |- imp(or(p0, p1), imp(not(p0), p1))
  |- imp(imp(not(p0), p1), or(p0, p1))
}
```

## 性质  

```follow
thm or.left(prop p0, prop p1) {
  |- imp(p0, or(p0, p1))
} = {
  syl(p0, or(p0,p1), imp(not(p0),p1))
  or.def(p0, p1)
  con2d(p0, p0, p1)
  a1(p0, not(p1))
}
```

```follow
thm or.lefti(prop p0, prop p1) {
  |- or(p0, p1)
  -| p0
} = {
  mp(or(p0,p1), p0)
  or.left(p0, p1)
}
```

```follow
thm or.leftd(prop p0, prop p1, prop p2) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, p1)
} = {
  syl(p0, or(p1,p2), p1)
  or.left(p1, p2)
}
```

```follow
thm or.right(prop p0, prop p1) {
  |- imp(p1, or(p0, p1))
} = {
  syl(p1, or(p0,p1), imp(not(p0),p1))
  or.def(p0, p1)
  a1(p1, not(p0))
}
```

```follow
thm or.righti(prop p0, prop p1) {
  |- or(p0, p1)
  -| p1
} = {
  mp(or(p0,p1), p1)
  or.right(p0, p1)
}
```

```follow
thm or.rightd(prop p0, prop p1, prop p2) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, p2)
} = {
  syl(p0, or(p1,p2), p2)
  or.right(p1, p2)
}
```

```follow
thm or.rw2(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, or(p3, p2))
  -| imp(p3, p1)
} = {
  syl(p0, or(p1,p2), imp(not(p1),p2))
  or.def(p1, p2)
  syl(p0, imp(not(p1),p2), or(p3,p2))
  syl(or(p3,p2), imp(not(p1),p2), imp(not(p3),p2))
  or.def(p3, p2)
  mp(imp(imp(not(p3),p2),imp(not(p1),p2)), imp(not(p1),not(p3)))
  trans(not(p1), not(p3), p2)
  con3i(p1, p3)
}
```

```follow
thm or.rw3(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, or(p1, p3))
  -| imp(p3, p2)
} = {
  syl(p0, or(p1,p2), or(p1,p3))
  syl(or(p1,p3), or(p1,p2), imp(not(p1),p3))
  or.def(p1, p3)
  syl(imp(not(p1),p3), or(p1,p2), imp(not(p1),p2))
  or.def(p1, p2)
  mp(imp(imp(not(p1),p3),imp(not(p1),p2)), imp(p3,p2))
  trans(not(p1), p3, p2)
}
```

```follow
thm or.rw23(prop p0, prop p1, prop p2, prop p3, prop p4) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, or(p3, p4))
  -| imp(p3, p1)
  -| imp(p4, p2)
} = {
  or.rw2(p0, p1, p2, p3)
  or.rw3(p0, p3, p2, p4)
}
```

## 交换率  

```follow
thm or.com(prop p0, prop p1) {
  |- imp(or(p0, p1), or(p1, p0))
} = {
  syl(or(p0,p1), or(p1,p0), imp(not(p0),p1))
  or.def(p0, p1)
  syl(imp(not(p0),p1), or(p1,p0), imp(not(p1),p0))
  or.def(p1, p0)
  con2(p0, p1)
}
```

```follow
thm or.comi(prop p0, prop p1) {
  |- or(p0, p1)
  -| or(p1, p0)
} = {
  mp(or(p0,p1), or(p1,p0))
  or.com(p1, p0)
}
```

```follow
thm or.comd(prop p0, prop p1, prop p2) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, or(p2, p1))
} = {
  syl(p0, or(p1,p2), or(p2,p1))
  or.com(p2, p1)
}
```

## 分类讨论 `or.cases`  

```follow
thm or.cases(prop p0, prop p1, prop p2) {
  |- imp(imp(p0, p2), imp(imp(p1, p2), imp(or(p0, p1), p2)))
  |- imp(imp(p1, p2), imp(imp(p0, p2), imp(or(p0, p1), p2)))
} = {
  com12i(imp(p1,p2), imp(p0,p2), imp(or(p0,p1),p2))
  and.expoi(imp(p0,p2), imp(p1,p2), imp(or(p0,p1),p2))
  rw2(and(imp(p0,p2),imp(p1,p2)), or(p0,p1), p2, imp(not(p0),p1))
  or.def(p0, p1)
  and.impoi(imp(p0,p2), imp(p1,p2), imp(imp(not(p0),p1),p2))
  com12i(imp(p0,p2), imp(p1,p2), imp(imp(not(p0),p1),p2))
  com23i(imp(p1,p2), imp(p0,p2), imp(not(p0),p1), p2)
  rw3(imp(p1,p2), imp(not(p0),p1), imp(imp(p0,p2),p2), imp(not(p0),p2))
  cont(p0, p2)
  trans(not(p0), p1, p2)
}
```

```follow
thm or.casesii(prop p0, prop p1, prop p2) {
  |- imp(or(p0, p1), p2)
  -| imp(p0, p2)
  -| imp(p1, p2)
} = {
  mp(imp(or(p0,p1),p2), imp(p1,p2))
  mp(imp(imp(p1,p2),imp(or(p0,p1),p2)), imp(p0,p2))
  or.cases(p0, p1, p2)
}
```

```follow
thm or.casesd(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, imp(or(p1, p2), p3))
  -| imp(p0, imp(p1, p3))
  -| imp(p0, imp(p2, p3))
} = {
  a2ii(p0, imp(or(p1,p2),p3), imp(p2,p3))
  syl(p0, imp(imp(p2,p3),imp(or(p1,p2),p3)), imp(p1,p3))
  or.cases(p1, p2, p3)
}
```

## `or` 和 `imp` 的关系

```follow
thm or.2or(prop p0, prop p1, prop p2, prop p3) {
  |- imp(imp(p0, p1), imp(imp(p2, p3), imp(or(p0, p2), or(p1, p3))))
} = {
  and.expoi(imp(p0,p1), imp(p2,p3), imp(or(p0,p2),or(p1,p3)))
  rw23(and(imp(p0,p1),imp(p2,p3)), or(p0,p2), or(p1,p3), imp(not(p0),p2), imp(not(p1),p3))
  or.def(p1, p3)
  or.def(p0, p2)
  and.impoi(imp(p0,p1), imp(p2,p3), imp(imp(not(p0),p2),imp(not(p1),p3)))
  syl(imp(p0,p1), imp(imp(p2,p3),imp(imp(not(p0),p2),imp(not(p1),p3))), imp(not(p1),not(p0)))
  trans4(not(p1), not(p0), p2, p3)
  con3(p0, p1)
}
```

```follow
thm or.2orii(prop p0, prop p1, prop p2, prop p3) {
  |- imp(or(p0, p1), or(p2, p3))
  -| imp(p0, p2)
  -| imp(p1, p3)
} = {
  mp(imp(or(p0,p1),or(p2,p3)), imp(p1,p3))
  mp(imp(imp(p1,p3),imp(or(p0,p1),or(p2,p3))), imp(p0,p2))
  or.2or(p0, p2, p1, p3)
}
```

```follow
thm or.2ord(prop p4, prop p0, prop p1, prop p2, prop p3) {
  |- imp(p4, imp(or(p0, p1), or(p2, p3)))
  -| imp(p4, imp(p0, p2))
  -| imp(p4, imp(p1, p3))
} = {
  mpd(p4, imp(or(p0,p1),or(p2,p3)), imp(p1,p3))
  syl(p4, imp(imp(p1,p3),imp(or(p0,p1),or(p2,p3))), imp(p0,p2))
  or.2or(p0, p2, p1, p3)
}
```

## `or` 和 `and` 的分配律  

```follow
thm or.and.1(prop p0, prop p1, prop p2) {
  |- imp(or(p0, and(p1, p2)), and(or(p0, p1), or(p0, p2)))
} = {
  and.introd(or(p0,and(p1,p2)), or(p0,p1), or(p0,p2))
  or.casesii(p0, and(p1,p2), or(p0,p1))
  or.left(p0, p1)
  syl(and(p1,p2), or(p0,p1), p1)
  or.right(p0, p1)
  and.left(p1, p2)
  or.casesii(p0, and(p1,p2), or(p0,p2))
  or.left(p0, p2)
  syl(and(p1,p2), or(p0,p2), p2)
  or.right(p0, p2)
  and.right(p1, p2)
}
```

```follow
thm or.and.2(prop p0, prop p1, prop p2) {
  |- imp(or(and(p0, p1), p2), and(or(p0, p2), or(p1, p2)))
} = {
  syl(or(and(p0,p1),p2), and(or(p0,p2),or(p1,p2)), or(p2,and(p0,p1)))
  or.com(and(p0,p1), p2)
  syl(or(p2,and(p0,p1)), and(or(p0,p2),or(p1,p2)), and(or(p2,p0),or(p2,p1)))
  or.and.1(p2, p0, p1)
  and.2andii(or(p2,p0), or(p0,p2), or(p2,p1), or(p1,p2))
  or.com(p2, p0)
  or.com(p2, p1)
}
```

```follow
thm or.and(prop p0, prop p1, prop p2) {
  |- imp(or(p0, and(p1, p2)), and(or(p0, p1), or(p0, p2)))
  |- imp(or(and(p0, p1), p2), and(or(p0, p2), or(p1, p2)))
} = {
  or.and.1(p0, p1, p2)
  or.and.2(p0, p1, p2)
}
```

```follow
thm and.or.1(prop p0, prop p1, prop p2) {
  |- imp(and(p0, or(p1, p2)), or(and(p0, p1), and(p0, p2)))
} = {
  and.impoi(p0, or(p1,p2), or(and(p0,p1),and(p0,p2)))
  com12i(p0, or(p1,p2), or(and(p0,p1),and(p0,p2)))
  or.casesii(p1, p2, imp(p0,or(and(p0,p1),and(p0,p2))))
  com12i(p1, p0, or(and(p0,p1),and(p0,p2)))
  and.expoi(p0, p1, or(and(p0,p1),and(p0,p2)))
  or.left(and(p0,p1), and(p0,p2))
  com12i(p2, p0, or(and(p0,p1),and(p0,p2)))
  and.expoi(p0, p2, or(and(p0,p1),and(p0,p2)))
  or.right(and(p0,p1), and(p0,p2))
}
```

```follow
thm and.or.2(prop p0, prop p1, prop p2) {
  |- imp(and(or(p0, p1), p2), or(and(p0, p2), and(p1, p2)))
} = {
  syl(and(or(p0,p1),p2), or(and(p0,p2),and(p1,p2)), and(p2,or(p0,p1)))
  and.com(or(p0,p1), p2)
  syl(and(p2,or(p0,p1)), or(and(p0,p2),and(p1,p2)), or(and(p2,p0),and(p2,p1)))
  and.or.1(p2, p0, p1)
  or.2orii(and(p2,p0), and(p2,p1), and(p0,p2), and(p1,p2))
  and.com(p2, p0)
  and.com(p2, p1)
}
```

```follow
thm and.or(prop p0, prop p1, prop p2) {
  |- imp(and(p0, or(p1, p2)), or(and(p0, p1), and(p0, p2)))
  |- imp(and(or(p0, p1), p2), or(and(p0, p2), and(p1, p2)))
} = {
  and.or.1(p0, p1, p2)
  and.or.2(p0, p1, p2)
}
```