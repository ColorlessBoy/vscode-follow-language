
// # 第一章 基本语法规则

// ## 关键词 `type` 

type prop // proposition 提议有对有错 

// ## 关键词 `term` 

term prop imp(prop p0, prop p1) { (p0 -> p1) } // imply 因此

// ## 关键词 `axiom` 

// 一阶逻辑第一条公理
axiom a1(prop p0, prop p1) {
  |- imp(p0,imp(p1,p0))
}

// 一阶逻辑第二条公理
axiom a2(prop p0, prop p1, prop p2) {
  |- imp(imp(p0,imp(p1,p2)), imp(imp(p0,p1),imp(p0,p2)))
}

// 一阶逻辑第三条公理
axiom mp(prop p0, prop p1) {
  |- p0
  -| imp(p1, p0)
  -| p1
}

// ## 关键词 `thm` 

thm a1i(prop p0, prop p1) {
  |- imp(p1, p0)
  -| p0
} = {
  mp(imp(p1,p0), p0)
  a1(p0, p1)
}
thm a2i(prop p0, prop p1, prop p2) {
  |- imp(imp(p0,p1), imp(p0,p2))
  -| imp(p0, imp(p1,p2))
} = {
  mp(imp(imp(p0,p1),imp(p0,p2)), imp(p0,imp(p1,p2)))
  a2(p0, p1, p2)
}
thm a2ii(prop p0, prop p1, prop p2) {
  |- imp(p0,p1)
  -| imp(p0,p2)
  -| imp(p0,imp(p2,p1))
} = {
  mp(imp(p0,p1), imp(p0,p2))
  a2i(p0, p2, p1)
}
thm mpd(prop p0, prop p1, prop p2) {
  |- imp(p0,p1)
  -| imp(p0,p2)
  -| imp(p0,imp(p2,p1))
} = {
  a2ii(p0, p1, p2)
}
thm id(prop p0) {
  |- imp(p0, p0)
} = {
  mpd(p0, p0, imp(p0,p0))
  a1(p0, p0)
  a1(p0, imp(p0,p0))
}
thm idd(prop p0, prop p1) {
  |- imp(p0, imp(p1,p1))
} = {
  a1i(imp(p1,p1), p0)
  id(p1)
}

// 三段论 syllogism
thm syl(prop p0, prop p1, prop p2) {
  |- imp(p0, p1)
  -| imp(p0, p2)
  -| imp(p2, p1)
} = {
  mpd(p0, p1, p2)
  a1i(imp(p2,p1), p0)
}

thm a1d(prop p0, prop p1, prop p2) {
  |- imp(p0, imp(p1, p2))
  -| imp(p0, p2)
} = {
  syl(p0, imp(p1,p2), p2)
  a1(p2, p1)
}

thm a2d(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, imp(imp(p1,p2), imp(p1,p3)))
  -| imp(p0, imp(p1, imp(p2, p3)))
} = {
  syl(p0, imp(imp(p1,p2),imp(p1,p3)), imp(p1,imp(p2,p3)))
  a2(p1, p2, p3)
}

thm com12i(prop p0, prop p1, prop p2) {
  |- imp(p0, imp(p1, p2))
  -| imp(p1, imp(p0, p2))
} = {
  syl(p0, imp(p1,p2), imp(p1,p0))
  a1(p0, p1)
  a2i(p1, p0, p2)
}

thm com12(prop p0, prop p1, prop p2) {
  |- imp(imp(p0, imp(p1, p2)), imp(p1, imp(p0, p2)))
} = {
  com12i(imp(p0,imp(p1,p2)), p1, imp(p0,p2))
  a2d(p1, p0, imp(p1,p2), p2)
  com12i(p1, p0, imp(imp(p1,p2),p2))
  a1i(imp(p1,imp(imp(p1,p2),p2)), p0)
  com12i(p1, imp(p1,p2), p2)
  id(imp(p1,p2))
}

thm trans.1(prop p0, prop p1, prop p2) {
  |- imp(imp(p0,p1), imp(imp(p1,p2), imp(p0,p2)))
} = {
  com12i(imp(p0,p1), imp(p1,p2), imp(p0,p2))
  a2d(imp(p1,p2), p0, p1, p2)
  a1(imp(p1,p2), p0)
}

thm trans(prop p0, prop p1, prop p2) {
  |- imp(imp(p0,p1), imp(imp(p1,p2), imp(p0,p2)))
  |- imp(imp(p1,p2), imp(imp(p0,p1), imp(p0,p2)))
} = {
  com12i(imp(p1,p2), imp(p0,p1), imp(p0,p2))
  trans.1(p0, p1, p2))
}

thm syl5(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, imp(p1,p2))
  -| imp(p0, imp(p3,p2))
  -| imp(p1, p3)
} = {
  syl(p0, imp(p1,p2), imp(p3,p2))
  mp(imp(imp(p3,p2),imp(p1,p2)), imp(p1,p3))
  trans(p1, p3, p2)
}

thm syl6(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, imp(p1,p2))
  -| imp(p0, imp(p1,p3))
  -| imp(p3, p2)
} = {
  syl(p0, imp(p1,p2), imp(p1,p3))
  mp(imp(imp(p1,p3),imp(p1,p2)), imp(p3,p2))
  trans(p1, p3, p2)
}