
# 等价符号 `=` 

```follow
term prop eq(class c0, class c1) { ( c0 = c1 )}
```

## 公理 `a6` 

```follow
axiom a6.1(set s0, set s1) {
  |- not(forall(s0, not(eq(s2c(s0), s2c(s1)))))
  |- exist(s0, eq(s2c(s0), s2c(s1)))
  diff (s0, s1)
}
```

### `exist.gen` 

```follow
thm exist.gen(set s0, prop p0) {
  |- exist(s0, p0)
  -| p0
} = {
  mp(exist(s0,p0), exist(s0,eq(s2c(s0),s2c(hs0))))
  a6.1(s0, hs0)
  a4.existi.gen(s0, eq(s2c(s0),s2c(hs0)), p0)
  a1i(eq(s2c(s0),s2c(hs0)), p0)
}
```

```follow
thm forall.2exist(set s0, prop p0) {
  |- imp(forall(s0, p0), exist(s0, p0))
} = {
  a4.exist2.1i(s0, p0, p0)
  exist.gen(s0, imp(p0,p0))
  id(p0)
}
```

```follow
thm forall.2existi(set s0, prop p0) {
  |- exist(s0, p0)
  -| forall(s0, p0) 
} = {
  mp(exist(s0,p0), forall(s0,p0))
  forall.2exist(s0, p0)
}
```

```follow
thm forall.2existd(set s0, prop p0, prop p1) {
  |- imp(p1, exist(s0, p0))
  -| imp(p1, forall(s0, p0))
} = {
  syl(p1, exist(s0,p0), forall(s0,p0))
  forall.2exist(s0, p0)
}
```

```follow
thm a5.exist.2(set s0, prop p0) {
  |- imp(p0, exist(s0, p0))
  diff (s0, p0)
} = {
  syl(p0, exist(s0,p0), forall(s0,p0))
  forall.2exist(s0, p0)
  a5(s0, p0)
}
```

```follow
thm a5.iff(set s0, prop p0) {
  |- iff(p0, exist(s0, p0))
  |- iff(exist(s0, p0), p0)
  |- imp(p0, exist(s0, p0))
  |- imp(exist(s0, p0), p0)

  |- iff(p0, forall(s0, p0))
  |- iff(forall(s0, p0), p0)
  |- imp(p0, forall(s0, p0))
  |- imp(forall(s0, p0), p0)
  diff (s0, p0)
} = {
  iff.introii(p0, exist(s0,p0))
  iff.introii(exist(s0,p0), p0)
  a5.exist.1(s0, p0)
  a5.exist.2(s0, p0)

  iff.introii(p0, forall(s0,p0))
  iff.introii(forall(s0,p0), p0)
  a5(s0, p0)
  syl(forall(s0,p0), p0, not(exist(s0,not(p0))))
  forall.notexist(s0, p0)
  con2i(exist(s0,not(p0)), p0)
  a5.exist.2(s0, not(p0))
}
```

### `exist.imp`

```follow
thm exist.imp.1(set s0, prop p1, prop p2) {
  |- imp(imp(exist(s0, p1), exist(s0, p2)), exist(s0, imp(p1, p2)))
} = {
  syl(imp(exist(s0,p1),exist(s0,p2)), exist(s0,imp(p1,p2)), imp(forall(s0,p1),exist(s0,p2)))
  a4.exist2(s0, p1, p2)
  mp(imp(imp(exist(s0,p1),exist(s0,p2)),imp(forall(s0,p1),exist(s0,p2))), imp(forall(s0,p1),exist(s0,p1)))
  trans(forall(s0,p1), exist(s0,p1), exist(s0,p2))
  forall.2exist(s0, p1)
}
```


```follow
thm exist.imp.2(set s0, prop p1, prop p2) {
  |- imp(imp(forall(s0, p1), forall(s0, p2)), exist(s0, imp(p1, p2)))
} = {
  syl(imp(forall(s0,p1),forall(s0,p2)), exist(s0,imp(p1,p2)), imp(forall(s0,p1),exist(s0,p2)))
  a4.exist2(s0, p1, p2)
  mp(imp(imp(forall(s0,p1),forall(s0,p2)),imp(forall(s0,p1),exist(s0,p2))), imp(forall(s0,p2),exist(s0,p2)))
  trans(forall(s0,p1), forall(s0,p2), exist(s0,p2))
  forall.2exist(s0, p2)
}
```

### `exist.or` 

```follow
thm exist.or(set s0, prop p1, prop p2) {
  |- imp(or(forall(s0, p1), exist(s0, p2)), exist(s0, or(p1, p2)))
} = {
  syl(or(forall(s0,p1),exist(s0,p2)), exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)))
  exist.orexist(s0, p1, p2)
  or.2orii(forall(s0,p1), exist(s0,p2), exist(s0,p1), exist(s0,p2))
  id(exist(s0,p2))
  forall.2exist(s0, p1)
}
```

### Specialization

```follow
thm forall.special.1(set s0, set s1, prop p0, prop p1) {
  |- imp(forall(s0, p0), p1)
  -| imp(not(p1), forall(s0, not(p1)))
  -| imp(eq(s2c(s0), s2c(s1)), imp(p0, p1))
  diff (s0, s1)
} = {
  mp(imp(forall(s0,p0),p1), exist(s0,eq(s2c(s0),s2c(s1))))
  a6.1(s0, s1)
  rw3(exist(s0,eq(s2c(s0),s2c(s1))), forall(s0,p0), p1, not(forall(s0,not(p1))))
  con2i(forall(s0,not(p1)), p1)
  rw3(exist(s0,eq(s2c(s0),s2c(s1))), forall(s0,p0), not(forall(s0,not(p1))), exist(s0,p1))
  exist.def(s0, p1)
  syl(exist(s0,eq(s2c(s0),s2c(s1))), imp(forall(s0,p0),exist(s0,p1)), exist(s0,imp(p0,p1)))
  a4.exist2(s0, p0, p1)
  a4.existi.gen(s0, eq(s2c(s0),s2c(s1)), imp(p0,p1))
}
```

```follow
thm forall.special.2(set s0, set s1, prop p0, prop p1) {
  |- imp(forall(s0, p0), p1)
  -| imp(eq(s2c(s0), s2c(s1)), imp(p0, p1))
  diff (s0, p1) (s0, s1)
} = {
  forall.special.1(s0, s1, p0, p1)
  a5(s0, not(p1))
}
```

```follow
thm forall.special.3(set s0, prop p1) {
  |- imp(forall(s0, p1), p1)
  -| imp(not(p1), forall(s0, not(p1)))
} = {
  forall.special.1(s0, hs0, p1, p1)
  idd(eq(s2c(s0),s2c(hs0)), p1)
}
```

```follow
thm forall.special.4(set s0, prop p1) {
  |- imp(forall(s0, p1), p1)
  -| not(p1)
} = {
  forall.special.3(s0, p1)
  a1i(not(p1), forall(s0,not(p1)))
  gen(s0, not(p1))
}
```

## `exist.mp`

```follow
thm exist.mp(set s0, prop p1, prop p2) {
  |- p1
  -| imp(p2, p1)
  -| exist(s0, p2)
  diff (s0, p1)
} = {
  mp(p1, exist(s0,p1))
  a5.iff(s0, p1)
  mp(exist(s0,p1), exist(s0,p2))
  a4.existi.gen(s0, p2, p1)
}
```

## 公理 `a7` 

公理`a7`涉及等号的有限的传递性。等号两边需要是`set`，并且穿入参数需要有两个 `diff` 限制。

```follow
axiom a7.1(set s0, set s1, set s2) {
  |- imp(eq(s2c(s0), s2c(s1)), imp(eq(s2c(s0), s2c(s2)), eq(s2c(s1), s2c(s2))))
  diff (s0, s1) (s0, s2)
}
```

```follow
axiom a7.2(set s0, set s1, set s2) {
  |- imp(eq(s2c(s0), s2c(s1)), imp(eq(s2c(s0), s2c(s2)), eq(s2c(s1), s2c(s2))))
  diff (s0, s1) (s1, s2)
}
```

```follow
// 有限的eq.id，只涉及set，不涉及class
thm eq.id(set s0) {
  |- eq(s2c(s0), s2c(s0))
} = {
  mp(eq(s2c(s0),s2c(s0)), exist(hs0,eq(s2c(s0),s2c(s0))))
  a5.iff(hs0, eq(s2c(s0),s2c(s0)))
  mp(exist(hs0,eq(s2c(s0),s2c(s0))), exist(hs0,eq(s2c(hs0),s2c(s0))))
  a6.1(hs0, s0)
  a4.existi.gen(hs0, eq(s2c(hs0),s2c(s0)), eq(s2c(s0),s2c(s0)))
  iid(eq(s2c(hs0),s2c(s0)), eq(s2c(s0),s2c(s0)))
  a7.1(hs0, s0, s0)
}
```

```follow
thm eq.com.1(set s0, set s1) {
  |- imp(eq(s2c(s0), s2c(s1)), eq(s2c(s1), s2c(s0)))
  diff (s0, s1)
} = {
  mp(imp(eq(s2c(s0),s2c(s1)),eq(s2c(s1),s2c(s0))), eq(s2c(s0),s2c(s0)))
  eq.id(s0)
  com12i(eq(s2c(s0),s2c(s0)), eq(s2c(s0),s2c(s1)), eq(s2c(s1),s2c(s0)))
  a7.2(s0, s1, s0)
}
```

```follow
thm a6.2(set s0, set s1) {
  |- not(forall(s0, not(eq(s2c(s0), s2c(s1)))))
  |- exist(s0, eq(s2c(s0), s2c(s1)))
  |- not(forall(s0, not(eq(s2c(s1), s2c(s0)))))
  |- exist(s0, eq(s2c(s1), s2c(s0)))
  diff (s0, s1)
} = {
  mp(not(forall(s0,not(eq(s2c(s1),s2c(s0))))), exist(s0,eq(s2c(s1),s2c(s0))))
  exist.def(s0, eq(s2c(s1),s2c(s0)))
  mp(exist(s0,eq(s2c(s1),s2c(s0))), exist(s0,eq(s2c(s0),s2c(s1))))
  a6.1(s0, s1)
  a4.existi.gen(s0, eq(s2c(s0),s2c(s1)), eq(s2c(s1),s2c(s0)))
  eq.com.1(s0, s1)
}
```

```follow
// 因为接受隐变量和公理a6，所以接受无限制的a7
thm a7(set s0, set s1, set s2) {
  |- imp(eq(s2c(s0), s2c(s1)), imp(eq(s2c(s0), s2c(s2)), eq(s2c(s1), s2c(s2))))
} = {
  mp(imp(eq(s2c(s0),s2c(s1)),imp(eq(s2c(s0),s2c(s2)),eq(s2c(s1),s2c(s2)))), exist(hs0,imp(eq(s2c(s0),s2c(s1)),imp(eq(s2c(s0),s2c(s2)),eq(s2c(s1),s2c(s2))))))
  a5.iff(hs0, imp(eq(s2c(s0),s2c(s1)),imp(eq(s2c(s0),s2c(s2)),eq(s2c(s1),s2c(s2)))))
  mp(exist(hs0,imp(eq(s2c(s0),s2c(s1)),imp(eq(s2c(s0),s2c(s2)),eq(s2c(s1),s2c(s2))))), exist(hs0,eq(s2c(s0),s2c(hs0))))
  a6.2(hs0, s0)
  a4.existi.gen(hs0, eq(s2c(s0),s2c(hs0)), imp(eq(s2c(s0),s2c(s1)),imp(eq(s2c(s0),s2c(s2)),eq(s2c(s1),s2c(s2)))))
  syld(eq(s2c(s0),s2c(hs0)), eq(s2c(s0),s2c(s1)), imp(eq(s2c(s0),s2c(s2)),eq(s2c(s1),s2c(s2))), eq(s2c(hs0),s2c(s1)))
  a7.2(s0, hs0, s1)
  com23i(eq(s2c(s0),s2c(hs0)), eq(s2c(hs0),s2c(s1)), eq(s2c(s0),s2c(s2)), eq(s2c(s1),s2c(s2)))
  syld(eq(s2c(s0),s2c(hs0)), eq(s2c(s0),s2c(s2)), imp(eq(s2c(hs0),s2c(s1)),eq(s2c(s1),s2c(s2))), eq(s2c(hs0),s2c(s2)))
  a7.2(s0, hs0, s2)
  a1i(eq(s2c(s0),s2c(hs0)), imp(eq(s2c(hs0),s2c(s2)),imp(eq(s2c(hs0),s2c(s1)),eq(s2c(s1),s2c(s2)))))
  com12i(eq(s2c(hs0),s2c(s2)), eq(s2c(hs0),s2c(s1)), eq(s2c(s1),s2c(s2)))
  a7.1(hs0, s1, s2)
}
```

```follow
thm eq.com(set s0, set s1) {
  |- iff(eq(s2c(s0), s2c(s1)), eq(s2c(s1), s2c(s0)))
  |- imp(eq(s2c(s0), s2c(s1)), eq(s2c(s1), s2c(s0)))
} = {
  iff.introii(eq(s2c(s0),s2c(s1)), eq(s2c(s1),s2c(s0)))
  mp(imp(eq(s2c(s0),s2c(s1)),eq(s2c(s1),s2c(s0))), eq(s2c(s0),s2c(s0)))
  eq.id(s0)
  com12i(eq(s2c(s0),s2c(s0)), eq(s2c(s0),s2c(s1)), eq(s2c(s1),s2c(s0)))
  a7(s0, s1, s0)
  mp(imp(eq(s2c(s1),s2c(s0)),eq(s2c(s0),s2c(s1))), eq(s2c(s1),s2c(s1)))
  eq.id(s1)
  com12i(eq(s2c(s1),s2c(s1)), eq(s2c(s1),s2c(s0)), eq(s2c(s0),s2c(s1)))
  a7(s1, s0, s1)
}
```

### `eq.trans`

```follow
// 终结 eq.trans 的讨论 
// 遗憾的是这个定理还不支持 class 
thm eq.trans(set s0, set s1, set s2) {
  // 第一组
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s0),s2c(s2)), eq(s2c(s1),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s0),s2c(s2)), eq(s2c(s2),s2c(s1))))
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s2),s2c(s0)), eq(s2c(s1),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s2),s2c(s0)), eq(s2c(s2),s2c(s1))))
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s1),s2c(s2)), eq(s2c(s0),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s1),s2c(s2)), eq(s2c(s2),s2c(s0))))
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s2),s2c(s1)), eq(s2c(s0),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s2),s2c(s1)), eq(s2c(s2),s2c(s0))))

  // 第二组
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s0),s2c(s2)), eq(s2c(s1),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s0),s2c(s2)), eq(s2c(s2),s2c(s1))))
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s2),s2c(s0)), eq(s2c(s1),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s2),s2c(s0)), eq(s2c(s2),s2c(s1))))
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s1),s2c(s2)), eq(s2c(s0),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s1),s2c(s2)), eq(s2c(s2),s2c(s0))))
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s2),s2c(s1)), eq(s2c(s0),s2c(s2))))
  |- imp(eq(s2c(s0),s2c(s1)), iff(eq(s2c(s2),s2c(s1)), eq(s2c(s2),s2c(s0))))
} = {
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s0),s2c(s2)), eq(s2c(s1),s2c(s2)))
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s0),s2c(s2)), eq(s2c(s2),s2c(s1)))
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s0)), eq(s2c(s1),s2c(s2)))
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s0)), eq(s2c(s2),s2c(s1)))
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s1),s2c(s2)), eq(s2c(s0),s2c(s2)))
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s1),s2c(s2)), eq(s2c(s2),s2c(s0)))
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s1)), eq(s2c(s0),s2c(s2)))
  iff.introd(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s1)), eq(s2c(s2),s2c(s0)))
  rw3(eq(s2c(s0),s2c(s1)), eq(s2c(s0),s2c(s2)), eq(s2c(s2),s2c(s1)), eq(s2c(s1),s2c(s2)))
  eq.com(s1, s2)
  rw2(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s0)), eq(s2c(s1),s2c(s2)), eq(s2c(s0),s2c(s2)))
  eq.com(s2, s0)
  rw23(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s0)), eq(s2c(s2),s2c(s1)), eq(s2c(s0),s2c(s2)), eq(s2c(s1),s2c(s2)))
  eq.com(s1, s2)
  eq.com(s2, s0)
  a7(s0, s1, s2)
  syl(eq(s2c(s0),s2c(s1)), imp(eq(s2c(s1),s2c(s2)),eq(s2c(s0),s2c(s2))), eq(s2c(s1),s2c(s0)))
  eq.com(s0, s1)
  rw13(eq(s2c(s0),s2c(s1)), eq(s2c(s1),s2c(s2)), eq(s2c(s2),s2c(s0)), eq(s2c(s1),s2c(s0)), eq(s2c(s0),s2c(s2)))
  eq.com(s0, s1)
  eq.com(s0, s2)
  rw12(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s1)), eq(s2c(s0),s2c(s2)), eq(s2c(s1),s2c(s0)), eq(s2c(s1),s2c(s2)))
  eq.com(s0, s1)
  eq.com(s2, s1)
  rw123(eq(s2c(s0),s2c(s1)), eq(s2c(s2),s2c(s1)), eq(s2c(s2),s2c(s0)), eq(s2c(s1),s2c(s0)), eq(s2c(s1),s2c(s2)), eq(s2c(s0),s2c(s2)))
  eq.com(s0, s1)
  eq.com(s2, s1)
  eq.com(s0, s2)
  a7(s1, s0, s2)
}
```

### `eq.exist`

```follow
// p1 = p0[s1/s0]
thm eq.exist.1(set s0, set s1, prop p0, prop p1) {
  |- iff(exist(s0, and(eq(s2c(s0), s2c(s1)), p0)), p1)
  -| imp(eq(s2c(s0), s2c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  iff.syl(exist(s0,and(eq(s2c(s0),s2c(s1)),p0)), p1, exist(s0,and(eq(s2c(s0),s2c(s1)),p1)))
  a4.exist.iffi.gen(s0, and(eq(s2c(s0),s2c(s1)),p0), and(eq(s2c(s0),s2c(s1)),p1))

}
```

```follow
thm eq.exist.2(set s0, set s1, set s2) {
  |- imp(eq(s2c(s0), s2c(s1)), exist(s2, and(eq(s2c(s0),s2c(s2)), eq(s2c(s1),s2c(s2)))))
  diff (s0, s2) (s1, s2)
} = {
  syl(eq(s2c(s0),s2c(s1)), exist(s2,and(eq(s2c(s0),s2c(s2)),eq(s2c(s1),s2c(s2)))), exist(s2,eq(s2c(s0),s2c(s1))))
  a5.iff(s2, eq(s2c(s0),s2c(s1)))
}
```