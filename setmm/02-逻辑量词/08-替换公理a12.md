

# 替换公理 `a12` 

```follow
axiom a12.1(prop p0, set s0, set s1) {
  |- imp(eq(c(s0), c(s1)), imp(p0, forall(s0, imp(eq(c(s0), c(s1)), p0))))
  diff (s1, p0) (s1, s0)
}
```

## 超出`follow`语言的证明  

原因和公理 `a10` 和 `a11` 一样， 这个公理在 `follow` 中不可证明，在一阶逻辑中可以通过前面的公理证明出来。

```follow
thm a12.weak.lemma.1(prop p0, set s0, prop p1, set s1) {
  |- imp(eq(c(s0),c(s1)), imp(p0, forall(s0, imp(eq(c(s0),c(s1)), p0))))
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  -| imp(p1, forall(s0, p1))
} = {
  rw3(eq(c(s0),c(s1)), p0, forall(s0,imp(eq(c(s0),c(s1)),p0)), p1)
  iff.leftd(eq(c(s0),c(s1)), p0, p1)
  syl(p1, forall(s0,imp(eq(c(s0),c(s1)),p0)), forall(s0,p1))
  a4.1i(s0, p1, imp(eq(c(s0),c(s1)),p0))
  gen(s0, imp(p1,imp(eq(c(s0),c(s1)),p0)))
  com12i(p1, eq(c(s0),c(s1)), p0)
  iff.rightd(eq(c(s0),c(s1)), p0, p1)
}
```

```follow
thm a12.weak.1(prop p0, set s0, prop p1, set s1) {
  |- imp(eq(c(s0),c(s1)), imp(p0, forall(s0, imp(eq(c(s0),c(s1)), p0))))
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  diff(s0, p1)
} = {
  a12.weak.lemma.1(p0, s0, p1, s1)
  a5(s0, p1)
}
```

```follow
thm a12.weak.2(prop p0, set s0, prop p1, set s1, prop p2, set s2) {
  |- imp(eq(c(s0),c(s1)), imp(forall(s1, p0), forall(s0, imp(eq(c(s0),c(s1)), p0))))
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  -| imp(eq(c(s1),c(s2)), iff(p0,p2))
  diff (s0, p1) (s1, s2) (s1, p2) (s2, p0)
} = {
  rw2(eq(c(s0),c(s1)), forall(s1,p0), forall(s0,imp(eq(c(s0),c(s1)),p0)), p0)
  a12.weak.1(p0, s0, p1, s1)
  forall.special.5(s1, p0, s2, p2)
}
```

```follow
thm a12.weak.3(prop p0, set s0, prop p1, set s1, prop p2, set s2) {
  |- imp(eq(c(s0),c(s0)), imp(forall(s0, p0), forall(s0, imp(eq(c(s0),c(s0)), p0))))
} = {
  a1i(eq(c(s0),c(s0)), imp(forall(s0,p0),forall(s0,imp(eq(c(s0),c(s0)),p0))))
  a4.1i(s0, p0, imp(eq(c(s0),c(s0)),p0))
  gen(s0, imp(p0,imp(eq(c(s0),c(s0)),p0)))
  a1(p0, eq(c(s0),c(s0)))
}
```

和前面的公理 `a10` 和 `a11` 相比复杂很多，这里需要一个示例来解释 `a12.weak.2` 怎么使用。 万幸的是我们可以直接使用 `a12`。

```follow
// 这个demo写起来非常复杂，lemma没有提取好，导致需要人工替换一系列非常复杂的等式。
thm a12.weak.demo(set s0, set s1, set s2) {
  |- imp(eq(c(s0),c(s1)), imp( 
    forall(s1, and(
      in(c(s0),c(s1)), 
      and(
        forall(s0, in(c(s2),c(s0))), 
        forall(s1,forall(s2,in(c(s1),c(s0))))
      )
    )),
    forall(s0, imp(eq(c(s0),c(s1)),and(
      in(c(s0),c(s1)), 
      and(
        forall(s0, in(c(s2),c(s0))), 
        forall(s1,forall(s2,in(c(s1),c(s0))))
      )
    ))
  )))
  diff (s0, s1, s2)
} = {
  a12.weak.2(and(in(c(s0),c(s1)), and(forall(s0,in(c(s2),c(s0))),forall(s1,forall(s2,in(c(s1),c(s0)))))), s0, and(in(c(s1),c(s1)), and(forall(hs0, in(c(s2),c(hs0))), forall(hs1,forall(s2,in(c(hs1),c(s1)))))), s1, and(in(c(s0),c(hs1)), and(forall(s0, in(c(s2),c(s0))), forall(hs1,forall(s2,in(c(hs1),c(s0)))))), hs1) 
  and.iffandd(eq(c(s0),c(s1)), in(c(s0),c(s1)), and(forall(s0,in(c(s2),c(s0))),forall(s1,forall(s2,in(c(s1),c(s0))))), in(c(s1),c(s1)), and(forall(hs0,in(c(s2),c(hs0))),forall(hs1,forall(s2,in(c(hs1),c(s1))))))
  a8(s0, s1, s1)
  and.iffandd(eq(c(s0),c(s1)), forall(s0,in(c(s2),c(s0))), forall(s1,forall(s2,in(c(s1),c(s0)))), forall(hs0,in(c(s2),c(hs0))), forall(hs1,forall(s2,in(c(hs1),c(s1)))))
  a1i(eq(c(s0),c(s1)), iff(forall(s0,in(c(s2),c(s0))),forall(hs0,in(c(s2),c(hs0)))))
  eq.forall.5(in(c(s2),c(s0)), s0, in(c(s2),c(hs0)), hs0)
  a9(s0, hs0, s2)
  iff.rw2(eq(c(s0),c(s1)), forall(s1,forall(s2,in(c(s1),c(s0)))), forall(hs1,forall(s2,in(c(hs1),c(s1)))), forall(hs1, forall(s2, in(c(hs1), c(s0)))))
  syl(eq(c(s0),c(s1)), iff(forall(hs1,forall(s2,in(c(hs1),c(s0)))),forall(hs1,forall(s2,in(c(hs1),c(s1))))), forall(hs1,iff(forall(s2,in(c(hs1),c(s0))),forall(s2,in(c(hs1),c(s1))))))
  a4(hs1, forall(s2,in(c(hs1),c(s0))), forall(s2,in(c(hs1),c(s1))))
  gend(hs1, iff(forall(s2,in(c(hs1),c(s0))),forall(s2,in(c(hs1),c(s1)))), eq(c(s0),c(s1)))
  syl(eq(c(s0),c(s1)), iff(forall(s2,in(c(hs1),c(s0))),forall(s2,in(c(hs1),c(s1)))), forall(s2,iff(in(c(hs1),c(s0)),in(c(hs1),c(s1)))))
  a4(s2, in(c(hs1),c(s0)), in(c(hs1),c(s1)))
  gend(s2, iff(in(c(hs1),c(s0)),in(c(hs1),c(s1))), eq(c(s0),c(s1)))
  a9(s0, s1, hs1)
  eq.forall.5(forall(s2,in(c(hs1),c(s0))), hs1, forall(s2,in(c(s1),c(s0))), s1)
  syl(eq(c(hs1),c(s1)), iff(forall(s2,in(c(hs1),c(s0))),forall(s2,in(c(s1),c(s0)))), forall(s2,iff(in(c(hs1),c(s0)),in(c(s1),c(s0)))))
  a4(s2, in(c(hs1),c(s0)), in(c(s1),c(s0)))
  gend(s2, iff(in(c(hs1),c(s0)),in(c(s1),c(s0))), eq(c(hs1),c(s1)))
  a8(hs1, s1, s0)
  and.iffandd(eq(c(s1),c(hs1)), in(c(s0),c(s1)), and(forall(s0,in(c(s2),c(s0))),forall(s1,forall(s2,in(c(s1),c(s0))))), in(c(s0),c(hs1)), and(forall(s0,in(c(s2),c(s0))),forall(hs1,forall(s2,in(c(hs1),c(s0))))))
  a9(s1, hs1, s0)
  and.iffandd(eq(c(s1),c(hs1)), forall(s0,in(c(s2),c(s0))), forall(s1,forall(s2,in(c(s1),c(s0)))), forall(s0,in(c(s2),c(s0))), forall(hs1,forall(s2,in(c(hs1),c(s0)))))
  iff.idd(eq(c(s1),c(hs1)), forall(s0,in(c(s2),c(s0))))
  a1i(eq(c(s1),c(hs1)), iff(forall(s1,forall(s2,in(c(s1),c(s0)))),forall(hs1,forall(s2,in(c(hs1),c(s0))))))
  eq.forall.5(forall(s2,in(c(s1),c(s0))), s1, forall(s2,in(c(hs1),c(s0))), hs1)
  syl(eq(c(s1),c(hs1)), iff(forall(s2,in(c(s1),c(s0))),forall(s2,in(c(hs1),c(s0)))), forall(s2,iff(in(c(s1),c(s0)),in(c(hs1),c(s0)))))
  a4(s2, in(c(s1),c(s0)), in(c(hs1),c(s0)))
  gend(s2, iff(in(c(s1),c(s0)),in(c(hs1),c(s0))), eq(c(s1),c(hs1)))
  a8(s1, hs1, s0)
}
```

## 公理等价形式

```follow
thm a12.2(prop p0, set s0, set s1) {
  |- imp(eq(c(s0), c(s1)), imp(p0, forall(s0, imp(eq(c(s0), c(s1)), p0))))
  diff (s1, s0)
} = {
  exist.mp(hs0, imp(eq(c(s0),c(s1)),imp(p0,forall(s0,imp(eq(c(s0),c(s1)),p0)))), eq(c(s1),c(hs0)))
  a6(hs0, s1)
  syld(eq(c(s1),c(hs0)), eq(c(s0),c(s1)), imp(p0,forall(s0,imp(eq(c(s0),c(s1)),p0))), eq(c(s0),c(hs0)))
  eq.trans(s1, hs0, s0)
  rw2(eq(c(s1),c(hs0)), eq(c(s0),c(hs0)), imp(p0,forall(s0,imp(eq(c(s0),c(s1)),p0))), imp(p0,forall(s0,imp(eq(c(s0),c(hs0)),p0))))
  a12.1(p0, s0, hs0)
  syl(eq(c(s1),c(hs0)), imp(imp(p0,forall(s0,imp(eq(c(s0),c(hs0)),p0))),imp(p0,forall(s0,imp(eq(c(s0),c(s1)),p0)))), imp(forall(s0,imp(eq(c(s0),c(hs0)),p0)),forall(s0,imp(eq(c(s0),c(s1)),p0))))
  trans(p0, forall(s0,imp(eq(c(s0),c(hs0)),p0)), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  a4.1d(s0, imp(eq(c(s0),c(hs0)),p0), imp(eq(c(s0),c(s1)),p0), eq(c(s1),c(hs0)))
  gend(s0, imp(imp(eq(c(s0),c(hs0)),p0),imp(eq(c(s0),c(s1)),p0)), eq(c(s1),c(hs0)))
  syl(eq(c(s1),c(hs0)), imp(imp(eq(c(s0),c(hs0)),p0),imp(eq(c(s0),c(s1)),p0)), imp(eq(c(s0),c(s1)),eq(c(s0),c(hs0))))
  trans(eq(c(s0),c(s1)), eq(c(s0),c(hs0)), p0)
  eq.trans(s1, hs0, s0)
}
```

```follow
thm a12.3(prop p0, set s0, set s1) {
  |- imp(eq(c(s1), c(s0)), imp(p0, forall(s0, imp(eq(c(s0), c(s1)), p0))))
  diff (s1, s0)
} = {
  syl(eq(c(s1),c(s0)), imp(p0,forall(s0,imp(eq(c(s0),c(s1)),p0))), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  a12.2(p0, s0, s1)
}
```

```follow
thm a12.4(prop p0, set s0, set s1) {
  |- imp(eq(c(s0), c(s1)), imp(p0, forall(s0, imp(eq(c(s1), c(s0)), p0))))
  diff (s1, s0)
} = {
  syl(eq(c(s0),c(s1)), imp(p0,forall(s0,imp(eq(c(s1),c(s0)),p0))), imp(p0,forall(s0,imp(eq(c(s0),c(s1)),p0))))
  a12.2(p0, s0, s1)
  mp(imp(imp(p0,forall(s0,imp(eq(c(s0),c(s1)),p0))),imp(p0,forall(s0,imp(eq(c(s1),c(s0)),p0)))), imp(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(eq(c(s1),c(s0)),p0))))
  trans(p0, forall(s0,imp(eq(c(s0),c(s1)),p0)), forall(s0,imp(eq(c(s1),c(s0)),p0)))
  a4.1i(s0, imp(eq(c(s0),c(s1)),p0), imp(eq(c(s1),c(s0)),p0))
  gen(s0, imp(imp(eq(c(s0),c(s1)),p0),imp(eq(c(s1),c(s0)),p0)))
  mp(imp(imp(eq(c(s0),c(s1)),p0),imp(eq(c(s1),c(s0)),p0)), imp(eq(c(s1),c(s0)),eq(c(s0),c(s1))))
  trans(eq(c(s1),c(s0)), eq(c(s0),c(s1)), p0)
  eq.com(s1, s0)
}
```

```follow
thm a12.5(prop p0, set s0, set s1) {
  |- imp(eq(c(s1), c(s0)), imp(p0, forall(s0, imp(eq(c(s1), c(s0)), p0))))
  diff (s1, s0)
} = {
  syl(eq(c(s1),c(s0)), imp(p0,forall(s0,imp(eq(c(s1),c(s0)),p0))), eq(c(s0),c(s1)))
  a12.4(p0, s0, s1)
  eq.com(s1, s0)
}
```

```follow
thm a12(prop p0, set s0, set s1) {
  |- imp(eq(c(s0), c(s1)), imp(p0, forall(s0, imp(eq(c(s0), c(s1)), p0))))
  |- imp(eq(c(s1), c(s0)), imp(p0, forall(s0, imp(eq(c(s0), c(s1)), p0))))
  |- imp(eq(c(s0), c(s1)), imp(p0, forall(s0, imp(eq(c(s1), c(s0)), p0))))
  |- imp(eq(c(s1), c(s0)), imp(p0, forall(s0, imp(eq(c(s1), c(s0)), p0))))
  diff (s1, s0)
} = {
  a12.2(p0, s0, s1)
  a12.3(p0, s0, s1)
  a12.4(p0, s0, s1)
  a12.5(p0, s0, s1)
}
```

## `special`

```follow
thm special.1(prop p0, set s0) {
  |- imp(p0, exist(s0, p0))
} = {
  exist.mp(hs0, imp(p0,exist(s0,p0)), eq(c(s0),c(hs0)))
  a6(hs0, s0)
  rw3(eq(c(s0),c(hs0)), p0, exist(s0,p0), forall(s0,imp(eq(c(s0),c(hs0)),p0)))
  a12.2(p0, s0, hs0)
  mp(imp(forall(s0,imp(eq(c(s0),c(hs0)),p0)),exist(s0,p0)), exist(s0,eq(c(s0),c(hs0))))
  com12i(exist(s0,eq(c(s0),c(hs0))), forall(s0,imp(eq(c(s0),c(hs0)),p0)), exist(s0,p0))
  a4(s0, eq(c(s0),c(hs0)), p0)
  a6(s0, hs0)
}
```

```follow
thm special.1i(prop p0, set s0) {
  |- exist(s0, p0)
  -| p0
} = {
  mp(exist(s0,p0), p0)
  special.1(p0, s0)
}
```

```follow
thm special.1d(prop p0, set s0, prop p1) {
  |- imp(p1, exist(s0,p0))
  -| imp(p1, p0)
} = {
  syl(p1, exist(s0,p0), p0)
  special.1(p0, s0)
}
```


```follow
thm special.2(prop p0, set s0) {
  |- imp(forall(s0, p0), p0)
} = {
  syl(forall(s0,p0), p0, not(exist(s0,not(p0))))
  exist.def(s0, p0)
  con2i(exist(s0,not(p0)), p0)
  special.1(not(p0), s0)
}
```

```follow
thm special.2i(prop p0, set s0) {
  |- p0
  -| forall(s0,p0)
} = {
  mp(p0, forall(s0,p0))
  special.2(p0, s0)
}
```

```follow
thm special.2d(prop p0, set s0, prop p1) {
  |- imp(p1,p0)
  -| imp(p1,forall(s0,p0))
} = {
  syl(p1, p0, forall(s0,p0))
  special.2(p0, s0)
}
```

```follow
thm special.3(prop p0, set s0) {
  |- imp(forall(s0, p0), exist(s0, p0))
} = {
  syl(forall(s0,p0), exist(s0,p0), p0)
  special.2(p0, s0)
  special.1(p0, s0)
}
```

```follow
thm special.3i(prop p0, set s0) {
  |- exist(s0, p0)
  -| forall(s0, p0)
} = {
  mp(exist(s0,p0), forall(s0,p0))
  special.3(p0, s0)
}
```

```follow
thm special.3d(prop p0, set s0, prop p1) {
  |- imp(p1, exist(s0, p0))
  -| imp(p1, forall(s0, p0))
} = {
  syl(p1, exist(s0,p0), forall(s0,p0))
  special.3(p0, s0)
}
```

```follow
thm special(prop p0, set s0) {
  |- imp(p0, exist(s0, p0))
  |- imp(forall(s0, p0), p0)
  |- imp(forall(s0, p0), exist(s0, p0))
} = {
  special.1(p0, s0)
  special.2(p0, s0)
  special.3(p0, s0)
}
````

```follow
// 酒馆悖论：存在一个酒馆，他里面的顾客满足，有一个人喝酒了，其他人都喝酒了
thm exist.special(prop p0, set s0) {
  |- exist(s0, imp(p0, forall(s0, p0)))
} = {
  mp(exist(s0,imp(p0,forall(s0,p0))), imp(forall(s0,p0),exist(s0,forall(s0,p0))))
  a4(s0, p0, forall(s0,p0))
  special.1(forall(s0,p0), s0)
}
```

## `notfree`

### `a5` 相关的讨论 

```follow
thm nf.a5.1(prop p0, set s0) {
  |- imp(nf(s0,p0),imp(p0,forall(s0,p0)))
} = {
  rw2(nf(s0,p0), p0, forall(s0,p0), exist(s0,p0))
  nf.def.5(s0, p0)
  special(p0, s0)
}
```

```follow
thm nf.a5.1i(prop p0, set s0) {
  |- imp(p0,forall(s0,p0))
  -| nf(s0,p0)
} = {
  mp(imp(p0,forall(s0,p0)), nf(s0,p0))
  nf.a5.1(p0, s0)
}
```

```follow
thm nf.a5.1d(prop p0, set s0, prop p1) {
  |- imp(p1,imp(p0,forall(s0,p0)))
  -| imp(p1,nf(s0,p0))
} = {
  syl(p1, imp(p0,forall(s0,p0)), nf(s0,p0))
  nf.a5.1(p0, s0)
}
```

```follow
thm nf.impii2(set s0, prop p1, prop p2) {
  |- nf(s0, imp(p1, p2))
  -| nf(s0, p1)
  -| imp(p1, nf(s0,p2))
} = {
  mp(nf(s0,imp(p1,p2)), nf(s0,p1))
  syl(nf(s0,p1), nf(s0,imp(p1,p2)), or(forall(s0,p1),forall(s0,not(p1))))
  nf.def.5(s0, p1)
  or.casesii(forall(s0,p1), forall(s0,not(p1)), nf(s0,imp(p1,p2)))
  nf.impd(s0, p1, p2, forall(s0,p1))
  nf.intro.1(s0, p1)
  syl(forall(s0,p1), nf(s0,p2), p1)
  special(p1, s0)
  syl(forall(s0,not(p1)), nf(s0,imp(p1,p2)), forall(s0,imp(p1,p2)))
  nf.intro.1(s0, imp(p1,p2))
  a4.1i(s0, not(p1), imp(p1,p2))
  gen(s0, imp(not(p1),imp(p1,p2)))
  cont(p1, p2)
}
```

```follow
thm nf.andii2(set s0, prop p1, prop p2) {
  |- nf(s0, and(p1, p2))
  -| nf(s0, p1)
  -| imp(p1, nf(s0,p2))
} = {
  nf.rw(s0, and(p1,p2), not(imp(p1,not(p2))))
  iff.and.def(p1, p2)
  nf.noti(s0, imp(p1,not(p2)))
  nf.impii2(s0, p1, not(p2))
  nf.notd(s0, p2, p1)
}
```

```follow
thm nf.a5.2(prop p0, set s0) {
  |- imp(nf(s0,p0),iff(p0,forall(s0,p0)))
} = {
  iff.introd(nf(s0,p0), p0, forall(s0,p0))
  nf.a5.1(p0, s0)
  a1i(nf(s0,p0), imp(forall(s0,p0),p0))
  special(p0, s0)
}
```

```follow
thm nf.a5.3(prop p0, set s0) {
  |- imp(nf(s0,p0),iff(exist(s0,p0),p0))
} = {
  iff.introd(nf(s0,p0), exist(s0,p0), p0)
  rw3(nf(s0,p0), exist(s0,p0), p0, forall(s0,p0))
  nf.def.5(s0, p0)
  a1i(nf(s0,p0), imp(p0,exist(s0,p0)))
  special(p0, s0)
}
```

```follow
thm nf.a5(prop p0, set s0) {
  // 第一组 
  |- imp(nf(s0,p0),iff(p0,forall(s0,p0)))
  |- imp(nf(s0,p0),iff(forall(s0,p0),p0))
  |- imp(nf(s0,p0),imp(p0,forall(s0,p0)))
  |- imp(nf(s0,p0),imp(forall(s0,p0),p0))
  // 第二组 
  |- imp(nf(s0,p0),iff(exist(s0,p0),p0))
  |- imp(nf(s0,p0),iff(p0, exist(s0,p0)))
  |- imp(nf(s0,p0),imp(exist(s0,p0),p0))
  |- imp(nf(s0,p0),imp(p0, exist(s0,p0)))
} = {
  iff.comd(nf(s0,p0), forall(s0,p0), p0)
  iff.leftd(nf(s0,p0), p0, forall(s0,p0))
  iff.rightd(nf(s0,p0), p0, forall(s0,p0))
  iff.comd(nf(s0,p0), p0, exist(s0,p0))
  iff.leftd(nf(s0,p0), exist(s0,p0), p0)
  iff.rightd(nf(s0,p0), exist(s0,p0), p0)
  nf.a5.2(p0, s0)
  nf.a5.3(p0, s0)
}
```

```follow
thm nf.a5i(prop p0, set s0) {
  // 第一组 
  |- iff(p0,forall(s0,p0))
  |- iff(forall(s0,p0),p0)
  |- imp(p0,forall(s0,p0))
  |- imp(forall(s0,p0),p0)
  // 第二组 
  |- iff(exist(s0,p0),p0)
  |- iff(p0, exist(s0,p0))
  |- imp(exist(s0,p0),p0)
  |- imp(p0, exist(s0,p0))
  -| nf(s0, p0)
} = {
  mp(iff(p0,forall(s0,p0)), nf(s0,p0))
  mp(iff(forall(s0,p0),p0), nf(s0,p0))
  mp(imp(p0,forall(s0,p0)), nf(s0,p0))
  mp(imp(forall(s0,p0),p0), nf(s0,p0))
  mp(iff(exist(s0,p0),p0), nf(s0,p0))
  mp(iff(p0,exist(s0,p0)), nf(s0,p0))
  mp(imp(exist(s0,p0),p0), nf(s0,p0))
  mp(imp(p0,exist(s0,p0)), nf(s0,p0))
  nf.a5(p0, s0)
}
```

### `a4` 相关讨论 

#### 普通形式 

```follow
thm nf.a4.3(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), iff(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1))))
} = {
  iff.comd(nf(s0,p0), imp(p0,forall(s0,p1)), forall(s0,imp(p0,p1)))
  iff.leftd(nf(s0,p0), forall(s0,imp(p0,p1)), imp(p0,forall(s0,p1)))
  iff.rightd(nf(s0,p0), forall(s0,imp(p0,p1)), imp(p0,forall(s0,p1)))
  iff.syld(nf(s0,p0), forall(s0,imp(p0,p1)), imp(p0,forall(s0,p1)), imp(exist(s0,p0),forall(s0,p1)))
  nf.a4.1(s0, p0, p1)
  imp.iffimpd(nf(s0,p0), exist(s0,p0), p0, forall(s0,p1), forall(s0,p1))
  iff.idd(nf(s0,p0), forall(s0,p1))
  nf.a5(p0, s0)
}
```

```follow
thm nf.a4.4(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0))))
  |- imp(nf(s0,p0), imp(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0))))
} = {
  iff.comd(nf(s0,p0), imp(exist(s0,p1),p0), forall(s0,imp(p1,p0)))
  iff.leftd(nf(s0,p0), forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0))
  iff.rightd(nf(s0,p0), forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0))
  iff.syld(nf(s0,p0), forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0), imp(exist(s0,p1),forall(s0,p0)))
  nf.a4.2(s0, p1, p0)
  imp.iffimpd(nf(s0,p0), exist(s0,p1), exist(s0,p1), forall(s0,p0), p0)
  iff.idd(nf(s0,p0), exist(s0,p1))
  nf.a5(p0, s0)
}
```

```follow
thm nf.a4.1234(set s0, prop p0, prop p1) {
  // 第一组
  |- imp(nf(s0,p0), iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p0), iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  // 第二组
  |- imp(nf(s0,p1), iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p1), iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  |- imp(nf(s0,p1), imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p1), imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  // 第三组
  |- imp(nf(s0,p0), iff(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), iff(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1))))
  // 第四组
  |- imp(nf(s0,p0), iff(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0))))
  |- imp(nf(s0,p0), imp(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0))))
} = {
  nf.a4.1(s0, p0, p1)
  nf.a4.2(s0, p0, p1)
  nf.a4.3(s0, p0, p1)
  nf.a4.4(s0, p0, p1)
}
```

```follow
thm nf.a4.1234i(set s0, prop p0, prop p1) {
  // 第一组
  |- iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  |- imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  // 第二组
  |- iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  |- imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  // 第三组
  |- iff(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1)))
  |- iff(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1)))
  |- imp(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1)))
  |- imp(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1)))
  // 第四组
  |- iff(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0))
  |- iff(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0)))
  |- imp(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0))
  |- imp(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0)))
  -| nf(s0, p0)
} = {
  mp(iff(imp(exist(s0,p0),forall(s0,p1)),forall(s0,imp(p0,p1))), nf(s0,p0))
  mp(iff(forall(s0,imp(p0,p1)),imp(exist(s0,p0),forall(s0,p1))), nf(s0,p0))
  mp(imp(imp(exist(s0,p0),forall(s0,p1)),forall(s0,imp(p0,p1))), nf(s0,p0))
  mp(imp(forall(s0,imp(p0,p1)),imp(exist(s0,p0),forall(s0,p1))), nf(s0,p0))
  mp(iff(forall(s0,imp(p0,p1)),imp(p0,forall(s0,p1))), nf(s0,p0))
  mp(iff(imp(p0,forall(s0,p1)),forall(s0,imp(p0,p1))), nf(s0,p0))
  mp(imp(forall(s0,imp(p0,p1)),imp(p0,forall(s0,p1))), nf(s0,p0))
  mp(imp(imp(p0,forall(s0,p1)),forall(s0,imp(p0,p1))), nf(s0,p0))
  mp(iff(forall(s0,imp(p1,p0)),imp(exist(s0,p1),p0)), nf(s0,p0))
  mp(iff(imp(exist(s0,p1),p0),forall(s0,imp(p1,p0))), nf(s0,p0))
  mp(imp(forall(s0,imp(p1,p0)),imp(exist(s0,p1),p0)), nf(s0,p0))
  mp(imp(imp(exist(s0,p1),p0),forall(s0,imp(p1,p0))), nf(s0,p0))
  nf.a4.1234(s0, p0, p1)
}
```

#### `iff`

```follow
thm nf.a4.iff.1(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(forall(s0,p1), p0)))
} = {
  rw3(nf(s0,p0), forall(s0,iff(p0,p1)), iff(forall(s0,p1),p0), iff(p0,forall(s0,p1)))
  iff.com(p0, forall(s0,p1))
  rw3(nf(s0,p0), forall(s0,iff(p0,p1)), imp(p0,forall(s0,p1)), iff(p0,forall(s0,p1)))
  iff.left(p0, forall(s0,p1))
  rw3(nf(s0,p0), forall(s0,iff(p0,p1)), imp(forall(s0,p1),p0), iff(p0,forall(s0,p1)))
  iff.right(p0, forall(s0,p1))

  syl(nf(s0,p0), imp(forall(s0,iff(p0,p1)),iff(p0,forall(s0,p1))), iff(p0,forall(s0,p0)))
  nf.a5(p0, s0)
  com12i(iff(p0,forall(s0,p0)), forall(s0,iff(p0,p1)), iff(p0,forall(s0,p1)))
  syl(forall(s0,iff(p0,p1)), imp(iff(p0,forall(s0,p0)),iff(p0,forall(s0,p1))), iff(forall(s0,p0),forall(s0,p1)))
  iff.trans(forall(s0,p0), forall(s0,p1), p0)
  a4(s0, p0, p1)
}
```

```follow
thm nf.a4.iff.2(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(p0, forall(s0,p1))))
} = {
  rw3(nf(s0,p0), forall(s0,iff(p1,p0)), iff(p0,forall(s0,p1)), iff(forall(s0,p1),p0))
  iff.com(forall(s0,p1), p0)
  rw3(nf(s0,p0), forall(s0,iff(p1,p0)), imp(forall(s0,p1),p0), iff(forall(s0,p1),p0))
  iff.left(forall(s0,p1), p0)
  rw3(nf(s0,p0), forall(s0,iff(p1,p0)), imp(p0,forall(s0,p1)), iff(forall(s0,p1),p0))
  iff.right(forall(s0,p1), p0)
  syl(nf(s0,p0), imp(forall(s0,iff(p1,p0)),iff(forall(s0,p1),p0)), iff(forall(s0,p0),p0))
  nf.a5(p0, s0)
  com12i(iff(forall(s0,p0),p0), forall(s0,iff(p1,p0)), iff(forall(s0,p1),p0))
  syl(forall(s0,iff(p1,p0)), imp(iff(forall(s0,p0),p0),iff(forall(s0,p1),p0)), iff(forall(s0,p1),forall(s0,p0)))
  iff.trans(forall(s0,p1), forall(s0,p0), p0)
  a4(s0, p1, p0)
}
```

```follow
thm nf.a4.iff.3(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(p0, exist(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(exist(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(p0, exist(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(exist(s0,p1), p0)))
} = {
  rw3(nf(s0,p0), forall(s0,iff(p0,p1)), iff(exist(s0,p1),p0), iff(p0,exist(s0,p1)))
  iff.com(p0, exist(s0,p1))
  rw3(nf(s0,p0), forall(s0,iff(p0,p1)), imp(p0,exist(s0,p1)), iff(p0,exist(s0,p1)))
  iff.left(p0, exist(s0,p1))
  rw3(nf(s0,p0), forall(s0,iff(p0,p1)), imp(exist(s0,p1),p0), iff(p0,exist(s0,p1)))
  iff.right(p0, exist(s0,p1))
  syl(nf(s0,p0), imp(forall(s0,iff(p0,p1)),iff(p0,exist(s0,p1))), iff(exist(s0,p0),p0))
  nf.a5(p0, s0)
  rw2(iff(exist(s0,p0),p0), forall(s0,iff(p0,p1)), iff(p0,exist(s0,p1)), iff(exist(s0,p0),exist(s0,p1)))
  iff.trans(exist(s0,p0), p0, exist(s0,p1))
  a4(s0, p0, p1)
}
```

```follow
thm nf.a4.iff.4(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(exist(s0, p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(p0, exist(s0, p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(exist(s0, p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(p0, exist(s0, p1))))
} = {
  rw3(nf(s0,p0), forall(s0,iff(p1,p0)), iff(p0,exist(s0,p1)), iff(exist(s0,p1),p0))
  iff.com(exist(s0,p1), p0)
  rw3(nf(s0,p0), forall(s0,iff(p1,p0)), imp(exist(s0,p1),p0), iff(exist(s0,p1),p0))
  iff.left(exist(s0,p1), p0)
  rw3(nf(s0,p0), forall(s0,iff(p1,p0)), imp(p0,exist(s0,p1)), iff(exist(s0,p1),p0))
  iff.right(exist(s0,p1), p0)
  syl(nf(s0,p0), imp(forall(s0,iff(p1,p0)),iff(exist(s0,p1),p0)), iff(exist(s0,p0),p0))
  nf.a5(p0, s0)
  rw2(iff(exist(s0,p0),p0), forall(s0,iff(p1,p0)), iff(exist(s0,p1),p0), iff(exist(s0,p1),exist(s0,p0)))
  iff.trans(exist(s0,p0), p0, exist(s0,p1))
  a4(s0, p1, p0)
}
```

```follow
thm nf.a4.iff.1234(set s0, prop p0, prop p1) {
  // 第一组 
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(forall(s0,p1), p0)))
  // 第二组
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(p0, forall(s0,p1))))
  // 第三组 
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(p0, exist(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(exist(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(p0, exist(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(exist(s0,p1), p0)))
  // 第四组 
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(exist(s0, p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(p0, exist(s0, p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(exist(s0, p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(p0, exist(s0, p1))))
} = {
  nf.a4.iff.1(s0, p0, p1)
  nf.a4.iff.2(s0, p0, p1)
  nf.a4.iff.3(s0, p0, p1)
  nf.a4.iff.4(s0, p0, p1)
}
```

```follow
thm nf.a4.iff.1234i(set s0, prop p0, prop p1) {
  // 第一组 
  |- imp(forall(s0,iff(p0,p1)), iff(p0, forall(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), iff(forall(s0,p1), p0))
  |- imp(forall(s0,iff(p0,p1)), imp(p0, forall(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), imp(forall(s0,p1), p0))
  // 第二组
  |- imp(forall(s0,iff(p1,p0)), iff(forall(s0,p1), p0))
  |- imp(forall(s0,iff(p1,p0)), iff(p0, forall(s0,p1)))
  |- imp(forall(s0,iff(p1,p0)), imp(forall(s0,p1), p0))
  |- imp(forall(s0,iff(p1,p0)), imp(p0, forall(s0,p1)))
  // 第三组 
  |- imp(forall(s0,iff(p0,p1)), iff(p0, exist(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), iff(exist(s0,p1), p0))
  |- imp(forall(s0,iff(p0,p1)), imp(p0, exist(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), imp(exist(s0,p1), p0))
  // 第四组 
  |- imp(forall(s0,iff(p1,p0)), iff(exist(s0, p1), p0))
  |- imp(forall(s0,iff(p1,p0)), iff(p0, exist(s0, p1)))
  |- imp(forall(s0,iff(p1,p0)), imp(exist(s0, p1), p0))
  |- imp(forall(s0,iff(p1,p0)), imp(p0, exist(s0, p1)))
  -| nf(s0,p0)
} = {
  mp(imp(forall(s0,iff(p0,p1)),iff(p0,forall(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,iff(p0,p1)),iff(forall(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p0,p1)),imp(p0,forall(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,iff(p0,p1)),imp(forall(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),iff(forall(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),iff(p0,forall(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),imp(forall(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),imp(p0,forall(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,iff(p0,p1)),iff(p0,exist(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,iff(p0,p1)),iff(exist(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p0,p1)),imp(p0,exist(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,iff(p0,p1)),imp(exist(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),iff(exist(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),iff(p0,exist(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),imp(exist(s0,p1),p0)), nf(s0,p0))
  mp(imp(forall(s0,iff(p1,p0)),imp(p0,exist(s0,p1))), nf(s0,p0))
  nf.a4.iff.1234(s0, p0, p1)
}
```

#### `and` 

```follow
thm nf.a4.and.1(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,and(p0,p1)), and(p0, forall(s0,p1))))
} = {
  rw2(nf(s0,p0), forall(s0,and(p0,p1)), and(p0,forall(s0,p1)), and(forall(s0,p0),forall(s0,p1)))
  a4(s0, p0, p1)
  and.2andd(nf(s0,p0), forall(s0,p0), forall(s0,p1), p0, forall(s0,p1))
  idd(nf(s0,p0), forall(s0,p1))
  nf.a5(p0, s0)
}
```

```follow
thm nf.a4.and.2(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,and(p1,p0)), and(forall(s0,p1), p0)))
} = {
  rw2(nf(s0,p0), forall(s0,and(p1,p0)), and(forall(s0,p1),p0), and(forall(s0,p1),forall(s0,p0)))
  a4(s0, p1, p0)
  and.2andd(nf(s0,p0), forall(s0,p1), forall(s0,p0), forall(s0,p1), p0)
  idd(nf(s0,p0), forall(s0,p1))
  nf.a5(p0, s0)
}
```

```follow
thm nf.a4.and(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,and(p0,p1)), and(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,and(p1,p0)), and(forall(s0,p1), p0)))
} = {
  nf.a4.and.1(s0, p0, p1)
  nf.a4.and.2(s0, p0, p1)
}
```

```follow
thm nf.a4.andi(set s0, prop p0, prop p1) {
  |- imp(forall(s0,and(p0,p1)), and(p0, forall(s0,p1)))
  |- imp(forall(s0,and(p1,p0)), and(forall(s0,p1), p0))
  -| nf(s0,p0)
} = {
  mp(imp(forall(s0,and(p0,p1)),and(p0,forall(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,and(p1,p0)),and(forall(s0,p1),p0)), nf(s0,p0))
  nf.a4.and(s0, p0, p1)
}
```

#### `exist`

```follow
thm nf.a4.exist.1(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1))))
} = {
  iff.comd(nf(s0,p0), imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1)))
  iff.leftd(nf(s0,p0), exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1)))
  iff.rightd(nf(s0,p0), exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1)))
  iff.rw2(nf(s0,p0), exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1)), imp(forall(s0,p0),exist(s0,p1)))
  a4(s0, p0, p1)
  imp.iffimp.rightd(nf(s0,p0), exist(s0,p1), forall(s0,p0), p0)
  nf.a5(p0, s0)
}
```

```follow
thm nf.a4.exist.2(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0))))
} = {
  iff.comd(nf(s0,p0), imp(forall(s0,p1),p0), exist(s0,imp(p1,p0)))
  iff.leftd(nf(s0,p0), exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0))
  iff.rightd(nf(s0,p0), exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0))
  iff.rw2(nf(s0,p0), exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0), imp(forall(s0,p1),exist(s0,p0)))
  a4(s0, p1, p0)
  imp.iffimp.leftd(nf(s0,p0), forall(s0,p1), exist(s0,p0), p0)
  nf.a5(p0, s0)
}
```

```follow
thm nf.a4.exist(set s0, prop p0, prop p1) {
  // 第一组
  |- imp(nf(s0,p0), iff(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1))))
  // 第二组
  |- imp(nf(s0,p0), iff(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0))))
} = {
  nf.a4.exist.1(s0, p0, p1)
  nf.a4.exist.2(s0, p0, p1)
}
```

```follow
thm nf.a4.existi(set s0, prop p0, prop p1) {
  // 第一组
  |- iff(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1)))
  |- iff(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1)))
  |- imp(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1)))
  |- imp(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1)))
  // 第二组
  |- iff(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0))
  |- iff(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0)))
  |- imp(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0))
  |- imp(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0)))
  -| nf(s0,p0)
} = {
  mp(iff(exist(s0,imp(p0,p1)),imp(p0,exist(s0,p1))), nf(s0,p0))
  mp(iff(imp(p0,exist(s0,p1)),exist(s0,imp(p0,p1))), nf(s0,p0))
  mp(imp(exist(s0,imp(p0,p1)),imp(p0,exist(s0,p1))), nf(s0,p0))
  mp(imp(imp(p0,exist(s0,p1)),exist(s0,imp(p0,p1))), nf(s0,p0))
  mp(iff(exist(s0,imp(p1,p0)),imp(forall(s0,p1),p0)), nf(s0,p0))
  mp(iff(imp(forall(s0,p1),p0),exist(s0,imp(p1,p0))), nf(s0,p0))
  mp(imp(exist(s0,imp(p1,p0)),imp(forall(s0,p1),p0)), nf(s0,p0))
  mp(imp(imp(forall(s0,p1),p0),exist(s0,imp(p1,p0))), nf(s0,p0))
  nf.a4.exist(s0, p0, p1)
}
```

#### `or` 

```follow
thm nf.a4.or.1(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,or(p0,p1)), or(p0, forall(s0,p1))))
} = {
  rw2(nf(s0,p0), forall(s0,or(p0,p1)), or(p0,forall(s0,p1)), forall(s0,imp(not(p0),p1)))
  a4.1i(s0, or(p0,p1), imp(not(p0),p1))
  gen(s0, imp(or(p0,p1),imp(not(p0),p1)))
  or.def(p0, p1)
  rw3(nf(s0,p0), forall(s0,imp(not(p0),p1)), or(p0,forall(s0,p1)), imp(not(p0),forall(s0,p1)))
  or.def(p0, forall(s0,p1))
  syl(nf(s0,p0), imp(forall(s0,imp(not(p0),p1)),imp(not(p0),forall(s0,p1))), nf(s0,not(p0)))
  nf.a4.3(s0, not(p0), p1)
  nf.not(s0, p0)
}
```

```follow
thm nf.a4.or.2(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,or(p1,p0)), or(forall(s0,p1), p0)))
} = {
  rw23(nf(s0,p0), forall(s0,or(p1,p0)), or(forall(s0,p1),p0), forall(s0,or(p0,p1)), or(p0,forall(s0,p1)))
  a4.1i(s0, or(p1,p0), or(p0,p1))
  gen(s0, imp(or(p1,p0),or(p0,p1)))
  or.com(p1, p0)
  or.com(p0, forall(s0,p1))
  nf.a4.or.1(s0, p0, p1)
}
```

```follow
thm nf.a4.or(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), imp(forall(s0,or(p0,p1)), or(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,or(p1,p0)), or(forall(s0,p1), p0)))
} = {
  nf.a4.or.1(s0, p0, p1)
  nf.a4.or.2(s0, p0, p1)
}
```

```follow
thm nf.a4.ori(set s0, prop p0, prop p1) {
  |- imp(forall(s0,or(p0,p1)), or(p0, forall(s0,p1)))
  |- imp(forall(s0,or(p1,p0)), or(forall(s0,p1), p0))
  -| nf(s0,p0)
} = {
  mp(imp(forall(s0,or(p0,p1)),or(p0,forall(s0,p1))), nf(s0,p0))
  mp(imp(forall(s0,or(p1,p0)),or(forall(s0,p1),p0)), nf(s0,p0))
  nf.a4.or(s0, p0, p1)
}
```

#### `exist.and` 

```follow
thm nf.a4.exist.and.1(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(exist(s0,and(p0,p1)), and(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(and(p0,exist(s0,p1)), exist(s0,and(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,and(p0,p1)), and(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(and(p0,exist(s0,p1)), exist(s0,and(p0,p1))))
} = {
  iff.comd(nf(s0,p0), and(p0,exist(s0,p1)), exist(s0,and(p0,p1)))
  iff.leftd(nf(s0,p0), exist(s0,and(p0,p1)), and(p0,exist(s0,p1)))
  iff.rightd(nf(s0,p0), exist(s0,and(p0,p1)), and(p0,exist(s0,p1)))
  iff.introd(nf(s0,p0), exist(s0,and(p0,p1)), and(p0,exist(s0,p1)))
  rw2(nf(s0,p0), exist(s0,and(p0,p1)), and(p0,exist(s0,p1)), and(exist(s0,p0),exist(s0,p1)))
  a4(s0, p0, p1)
  and.2andd(nf(s0,p0), exist(s0,p0), exist(s0,p1), p0, exist(s0,p1))
  idd(nf(s0,p0), exist(s0,p1))
  nf.a5(p0, s0)
  com12i(nf(s0,p0), and(p0,exist(s0,p1)), exist(s0,and(p0,p1)))
  and.impoi(p0, exist(s0,p1), imp(nf(s0,p0),exist(s0,and(p0,p1))))
  com23i(p0, exist(s0,p1), nf(s0,p0), exist(s0,and(p0,p1)))
  rw3(p0, nf(s0,p0), imp(exist(s0,p1),exist(s0,and(p0,p1))), forall(s0,imp(p1,and(p0,p1))))
  a4(s0, p1, and(p0,p1))
  com12i(p0, nf(s0,p0), forall(s0,imp(p1,and(p0,p1))))
  rw3(nf(s0,p0), p0, forall(s0,imp(p1,and(p0,p1))), forall(s0,p0))
  nf.a5(p0, s0)
  a4.1i(s0, p0, imp(p1,and(p0,p1)))
  gen(s0, imp(p0,imp(p1,and(p0,p1))))
  and.intro(p0, p1)
}
```

```follow
thm nf.a4.exist.and.2(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(exist(s0,and(p1,p0)), and(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(and(exist(s0,p1),p0), exist(s0,and(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,and(p1,p0)), and(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(and(exist(s0,p1),p0), exist(s0,and(p1,p0))))
} = {
  iff.comd(nf(s0,p0), and(exist(s0,p1),p0), exist(s0,and(p1,p0)))
  iff.leftd(nf(s0,p0), exist(s0,and(p1,p0)), and(exist(s0,p1),p0))
  iff.rightd(nf(s0,p0), exist(s0,and(p1,p0)), and(exist(s0,p1),p0))
  iff.rw3(nf(s0,p0), exist(s0,and(p1,p0)), and(exist(s0,p1),p0), and(p0,exist(s0,p1)))
  iff.and.com(p0, exist(s0,p1))
  iff.rw2(nf(s0,p0), exist(s0,and(p1,p0)), and(p0,exist(s0,p1)), exist(s0,and(p0,p1)))
  nf.a4.exist.and.1(s0, p0, p1)
  mp(iff(exist(s0,and(p0,p1)),exist(s0,and(p1,p0))), forall(s0,iff(and(p0,p1),and(p1,p0))))
  a4(s0, and(p0,p1), and(p1,p0))
  gen(s0, iff(and(p0,p1),and(p1,p0)))
  iff.and.com(p0, p1)
}
```

```follow
thm nf.a4.exist.and(set s0, prop p0, prop p1) {
  // 第一组
  |- imp(nf(s0,p0), iff(exist(s0,and(p0,p1)), and(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(and(p0,exist(s0,p1)), exist(s0,and(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,and(p0,p1)), and(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(and(p0,exist(s0,p1)), exist(s0,and(p0,p1))))
  // 第二组 
  |- imp(nf(s0,p0), iff(exist(s0,and(p1,p0)), and(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(and(exist(s0,p1),p0), exist(s0,and(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,and(p1,p0)), and(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(and(exist(s0,p1),p0), exist(s0,and(p1,p0))))
} = {
  nf.a4.exist.and.1(s0, p0, p1)
  nf.a4.exist.and.2(s0, p0, p1)
}
```

```follow
thm nf.a4.exist.andi(set s0, prop p0, prop p1) {
  // 第一组
  |- iff(exist(s0,and(p0,p1)), and(p0,exist(s0,p1)))
  |- iff(and(p0,exist(s0,p1)), exist(s0,and(p0,p1)))
  |- imp(exist(s0,and(p0,p1)), and(p0,exist(s0,p1)))
  |- imp(and(p0,exist(s0,p1)), exist(s0,and(p0,p1)))
  // 第二组 
  |- iff(exist(s0,and(p1,p0)), and(exist(s0,p1),p0))
  |- iff(and(exist(s0,p1),p0), exist(s0,and(p1,p0)))
  |- imp(exist(s0,and(p1,p0)), and(exist(s0,p1),p0))
  |- imp(and(exist(s0,p1),p0), exist(s0,and(p1,p0)))
  -| nf(s0,p0)
} = {
  mp(iff(exist(s0,and(p0,p1)),and(p0,exist(s0,p1))), nf(s0,p0))
  mp(iff(and(p0,exist(s0,p1)),exist(s0,and(p0,p1))), nf(s0,p0))
  mp(imp(exist(s0,and(p0,p1)),and(p0,exist(s0,p1))), nf(s0,p0))
  mp(imp(and(p0,exist(s0,p1)),exist(s0,and(p0,p1))), nf(s0,p0))
  mp(iff(exist(s0,and(p1,p0)),and(exist(s0,p1),p0)), nf(s0,p0))
  mp(iff(and(exist(s0,p1),p0),exist(s0,and(p1,p0))), nf(s0,p0))
  mp(imp(exist(s0,and(p1,p0)),and(exist(s0,p1),p0)), nf(s0,p0))
  mp(imp(and(exist(s0,p1),p0),exist(s0,and(p1,p0))), nf(s0,p0))
  nf.a4.exist.and(s0, p0, p1)
}
```

#### `exist.or` 

```follow
thm nf.a4.exist.or.1(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(exist(s0,or(p0,p1)), or(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(or(p0,exist(s0,p1)), exist(s0,or(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,or(p0,p1)), or(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(or(p0,exist(s0,p1)), exist(s0,or(p0,p1))))
} = {
  iff.comd(nf(s0,p0), or(p0,exist(s0,p1)), exist(s0,or(p0,p1)))
  iff.leftd(nf(s0,p0), exist(s0,or(p0,p1)), or(p0,exist(s0,p1)))
  iff.rightd(nf(s0,p0), exist(s0,or(p0,p1)), or(p0,exist(s0,p1)))
  iff.rw2(nf(s0,p0), exist(s0,or(p0,p1)), or(p0,exist(s0,p1)), or(exist(s0,p0),exist(s0,p1)))
  a4(s0, p0, p1)
  or.ifford(nf(s0,p0), exist(s0,p0), exist(s0,p1), p0, exist(s0,p1))
  nf.a5(p0, s0)
  iff.idd(nf(s0,p0), exist(s0,p1))
}
```

```follow
thm nf.a4.exist.or.2(set s0, prop p0, prop p1) {
  |- imp(nf(s0,p0), iff(exist(s0,or(p1,p0)), or(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(or(exist(s0,p1),p0), exist(s0,or(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,or(p1,p0)), or(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(or(exist(s0,p1),p0), exist(s0,or(p1,p0))))
} = {
  iff.comd(nf(s0,p0), or(exist(s0,p1),p0), exist(s0,or(p1,p0)))
  iff.leftd(nf(s0,p0), exist(s0,or(p1,p0)), or(exist(s0,p1),p0))
  iff.rightd(nf(s0,p0), exist(s0,or(p1,p0)), or(exist(s0,p1),p0))
  iff.rw2(nf(s0,p0), exist(s0,or(p1,p0)), or(exist(s0,p1),p0), or(exist(s0,p1),exist(s0,p0)))
  a4(s0, p1, p0)
  or.ifford(nf(s0,p0), exist(s0,p1), exist(s0,p0), exist(s0,p1), p0)
  iff.idd(nf(s0,p0), exist(s0,p1))
  nf.a5(p0, s0)
}
```

```follow
thm nf.a4.exist.or(set s0, prop p0, prop p1) {
  // 第一组
  |- imp(nf(s0,p0), iff(exist(s0,or(p0,p1)), or(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(or(p0,exist(s0,p1)), exist(s0,or(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,or(p0,p1)), or(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(or(p0,exist(s0,p1)), exist(s0,or(p0,p1))))

  // 第二组
  |- imp(nf(s0,p0), iff(exist(s0,or(p1,p0)), or(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(or(exist(s0,p1),p0), exist(s0,or(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,or(p1,p0)), or(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(or(exist(s0,p1),p0), exist(s0,or(p1,p0))))
} = {
  nf.a4.exist.or.1(s0, p0, p1)
  nf.a4.exist.or.2(s0, p0, p1)
}
```

```follow
thm nf.a4.exist.ori(set s0, prop p0, prop p1) {
  // 第一组
  |- iff(exist(s0,or(p0,p1)), or(p0,exist(s0,p1)))
  |- iff(or(p0,exist(s0,p1)), exist(s0,or(p0,p1)))
  |- imp(exist(s0,or(p0,p1)), or(p0,exist(s0,p1)))
  |- imp(or(p0,exist(s0,p1)), exist(s0,or(p0,p1)))

  // 第二组
  |- iff(exist(s0,or(p1,p0)), or(exist(s0,p1),p0))
  |- iff(or(exist(s0,p1),p0), exist(s0,or(p1,p0)))
  |- imp(exist(s0,or(p1,p0)), or(exist(s0,p1),p0))
  |- imp(or(exist(s0,p1),p0), exist(s0,or(p1,p0)))

  -| nf(s0,p0)
} = {
  mp(iff(exist(s0,or(p0,p1)),or(p0,exist(s0,p1))), nf(s0,p0))
  mp(iff(or(p0,exist(s0,p1)),exist(s0,or(p0,p1))), nf(s0,p0))
  mp(imp(exist(s0,or(p0,p1)),or(p0,exist(s0,p1))), nf(s0,p0))
  mp(imp(or(p0,exist(s0,p1)),exist(s0,or(p0,p1))), nf(s0,p0))
  mp(iff(exist(s0,or(p1,p0)),or(exist(s0,p1),p0)), nf(s0,p0))
  mp(iff(or(exist(s0,p1),p0),exist(s0,or(p1,p0))), nf(s0,p0))
  mp(imp(exist(s0,or(p1,p0)),or(exist(s0,p1),p0)), nf(s0,p0))
  mp(imp(or(exist(s0,p1),p0),exist(s0,or(p1,p0))), nf(s0,p0))
  nf.a4.exist.or(s0, p0, p1)
}
```

#### 汇总

```follow
thm nf.a4(set s0, prop p0, prop p1) {
  // nf.a4.1234 第一组
  |- imp(nf(s0,p0), iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p0), iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  // nf.a4.1234 第二组
  |- imp(nf(s0,p1), iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p1), iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  |- imp(nf(s0,p1), imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1))))
  |- imp(nf(s0,p1), imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1))))
  // nf.a4.1234 第三组
  |- imp(nf(s0,p0), iff(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), iff(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1))))
  // nf.a4.1234 第四组
  |- imp(nf(s0,p0), iff(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0))))
  |- imp(nf(s0,p0), imp(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0))))
  // nf.a4.iff.1234 第一组 
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(forall(s0,p1), p0)))
  // nf.a4.iff.1234 第二组
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(forall(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(p0, forall(s0,p1))))
  // nf.a4.iff.1234 第三组 
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(p0, exist(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), iff(exist(s0,p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(p0, exist(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p0,p1)), imp(exist(s0,p1), p0)))
  // nf.a4.iff.1234 第四组 
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(exist(s0, p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), iff(p0, exist(s0, p1))))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(exist(s0, p1), p0)))
  |- imp(nf(s0,p0), imp(forall(s0,iff(p1,p0)), imp(p0, exist(s0, p1))))
  // nf.a4.and
  |- imp(nf(s0,p0), imp(forall(s0,and(p0,p1)), and(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,and(p1,p0)), and(forall(s0,p1), p0)))
  // nf.a4.exist 第一组
  |- imp(nf(s0,p0), iff(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1))))
  // nf.a4.exist 第二组
  |- imp(nf(s0,p0), iff(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0))))
  // nf.a4.or 
  |- imp(nf(s0,p0), imp(forall(s0,or(p0,p1)), or(p0, forall(s0,p1))))
  |- imp(nf(s0,p0), imp(forall(s0,or(p1,p0)), or(forall(s0,p1), p0)))
  // nf.a4.exist.and 第一组
  |- imp(nf(s0,p0), iff(exist(s0,and(p0,p1)), and(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(and(p0,exist(s0,p1)), exist(s0,and(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,and(p0,p1)), and(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(and(p0,exist(s0,p1)), exist(s0,and(p0,p1))))
  // nf.a4.exist.and 第二组
  |- imp(nf(s0,p0), iff(exist(s0,and(p1,p0)), and(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(and(exist(s0,p1),p0), exist(s0,and(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,and(p1,p0)), and(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(and(exist(s0,p1),p0), exist(s0,and(p1,p0))))
  // nf.a4.exist.or 第一组
  |- imp(nf(s0,p0), iff(exist(s0,or(p0,p1)), or(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), iff(or(p0,exist(s0,p1)), exist(s0,or(p0,p1))))
  |- imp(nf(s0,p0), imp(exist(s0,or(p0,p1)), or(p0,exist(s0,p1))))
  |- imp(nf(s0,p0), imp(or(p0,exist(s0,p1)), exist(s0,or(p0,p1))))
  // nf.a4.exist.or 第二组
  |- imp(nf(s0,p0), iff(exist(s0,or(p1,p0)), or(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), iff(or(exist(s0,p1),p0), exist(s0,or(p1,p0))))
  |- imp(nf(s0,p0), imp(exist(s0,or(p1,p0)), or(exist(s0,p1),p0)))
  |- imp(nf(s0,p0), imp(or(exist(s0,p1),p0), exist(s0,or(p1,p0))))
} = {
  nf.a4.1234(s0,p0,p1)
  nf.a4.iff.1234(s0, p0, p1)
  nf.a4.and(s0, p0, p1)
  nf.a4.exist(s0, p0, p1)
  nf.a4.or(s0, p0, p1)
  nf.a4.exist.and(s0, p0, p1)
  nf.a4.exist.or(s0, p0, p1)
}
```

```follow
thm nf.a4i(set s0, prop p0, prop p1) {
  // nf.a4.1234 第一组
  |- iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  |- imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  // nf.a4.1234 第二组
  |- iff(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- iff(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  |- imp(imp(exist(s0,p0), forall(s0,p1)), forall(s0, imp(p0,p1)))
  |- imp(forall(s0, imp(p0,p1)), imp(exist(s0,p0), forall(s0,p1)))
  // nf.a4.1234 第三组
  |- iff(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1)))
  |- iff(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1)))
  |- imp(forall(s0,imp(p0,p1)), imp(p0, forall(s0,p1)))
  |- imp(imp(p0, forall(s0,p1)), forall(s0,imp(p0,p1)))
  // nf.a4.1234 第四组
  |- iff(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0))
  |- iff(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0)))
  |- imp(forall(s0,imp(p1,p0)), imp(exist(s0,p1),p0))
  |- imp(imp(exist(s0,p1),p0), forall(s0,imp(p1,p0)))
  // nf.a4.iff.1234 第一组 
  |- imp(forall(s0,iff(p0,p1)), iff(p0, forall(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), iff(forall(s0,p1), p0))
  |- imp(forall(s0,iff(p0,p1)), imp(p0, forall(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), imp(forall(s0,p1), p0))
  // nf.a4.iff.1234 第二组
  |- imp(forall(s0,iff(p1,p0)), iff(forall(s0,p1), p0))
  |- imp(forall(s0,iff(p1,p0)), iff(p0, forall(s0,p1)))
  |- imp(forall(s0,iff(p1,p0)), imp(forall(s0,p1), p0))
  |- imp(forall(s0,iff(p1,p0)), imp(p0, forall(s0,p1)))
  // nf.a4.iff.1234 第三组 
  |- imp(forall(s0,iff(p0,p1)), iff(p0, exist(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), iff(exist(s0,p1), p0))
  |- imp(forall(s0,iff(p0,p1)), imp(p0, exist(s0,p1)))
  |- imp(forall(s0,iff(p0,p1)), imp(exist(s0,p1), p0))
  // nf.a4.iff.1234 第四组 
  |- imp(forall(s0,iff(p1,p0)), iff(exist(s0, p1), p0))
  |- imp(forall(s0,iff(p1,p0)), iff(p0, exist(s0, p1)))
  |- imp(forall(s0,iff(p1,p0)), imp(exist(s0, p1), p0))
  |- imp(forall(s0,iff(p1,p0)), imp(p0, exist(s0, p1)))
  // nf.a4.and
  |- imp(forall(s0,and(p0,p1)), and(p0, forall(s0,p1)))
  |- imp(forall(s0,and(p1,p0)), and(forall(s0,p1), p0))
  // nf.a4.existi 第一组
  |- iff(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1)))
  |- iff(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1)))
  |- imp(exist(s0,imp(p0,p1)), imp(p0,exist(s0,p1)))
  |- imp(imp(p0,exist(s0,p1)), exist(s0,imp(p0,p1)))
  // nf.a4.existi 第二组
  |- iff(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0))
  |- iff(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0)))
  |- imp(exist(s0,imp(p1,p0)), imp(forall(s0,p1),p0))
  |- imp(imp(forall(s0,p1),p0), exist(s0,imp(p1,p0)))
  // nf.a4.or
  |- imp(forall(s0,or(p0,p1)), or(p0, forall(s0,p1)))
  |- imp(forall(s0,or(p1,p0)), or(forall(s0,p1), p0))
  // nf.a4.exist.and 第一组
  |- iff(exist(s0,and(p0,p1)), and(p0,exist(s0,p1)))
  |- iff(and(p0,exist(s0,p1)), exist(s0,and(p0,p1)))
  |- imp(exist(s0,and(p0,p1)), and(p0,exist(s0,p1)))
  |- imp(and(p0,exist(s0,p1)), exist(s0,and(p0,p1)))
  // nf.a4.exist.and 第二组
  |- iff(exist(s0,and(p1,p0)), and(exist(s0,p1),p0))
  |- iff(and(exist(s0,p1),p0), exist(s0,and(p1,p0)))
  |- imp(exist(s0,and(p1,p0)), and(exist(s0,p1),p0))
  |- imp(and(exist(s0,p1),p0), exist(s0,and(p1,p0)))
  // nf.a4.exist.or 第一组
  |- iff(exist(s0,or(p0,p1)), or(p0,exist(s0,p1)))
  |- iff(or(p0,exist(s0,p1)), exist(s0,or(p0,p1)))
  |- imp(exist(s0,or(p0,p1)), or(p0,exist(s0,p1)))
  |- imp(or(p0,exist(s0,p1)), exist(s0,or(p0,p1)))
  // nf.a4.exist.or 第二组
  |- iff(exist(s0,or(p1,p0)), or(exist(s0,p1),p0))
  |- iff(or(exist(s0,p1),p0), exist(s0,or(p1,p0)))
  |- imp(exist(s0,or(p1,p0)), or(exist(s0,p1),p0))
  |- imp(or(exist(s0,p1),p0), exist(s0,or(p1,p0)))
  -| nf(s0,p0)
} = {
  nf.a4.1234i(s0,p0,p1)
  nf.a4.iff.1234i(s0, p0, p1)
  nf.a4.andi(s0, p0, p1)
  nf.a4.existi(s0, p0, p1)
  nf.a4.ori(s0, p0, p1)
  nf.a4.exist.andi(s0, p0, p1)
  nf.a4.exist.ori(s0, p0, p1)
}
```

## `subs`

### `subs` 和 `eq`

```follow
thm subs.eq.3(prop p0, set s0, set s1) {
  |- imp(eq(c(s0),c(s1)), imp(p0, subs(p0,s0,s1)))
} = {
  rw3(eq(c(s0),c(s1)), p0, subs(p0,s0,s1), forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0)))))
  subs.def.2(p0, s0, s1, hs0)
  syl(eq(c(s0),c(s1)), imp(p0,forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))))), forall(hs0,imp(p0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))))))
  a4.diff(hs0, imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))), p0)
  gend(hs0, imp(p0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0)))), eq(c(s0),c(s1)))
  com23i(eq(c(s0),c(s1)), p0, eq(c(hs0),c(s1)), forall(s0,imp(eq(c(s0),c(hs0)),p0)))
  rw3(eq(c(s0),c(s1)), eq(c(hs0),c(s1)), imp(p0,forall(s0,imp(eq(c(s0),c(hs0)),p0))), eq(c(s0),c(hs0)))
  eq.trans(s0, s1, hs0)
  a12.2(p0, s0, hs0)
}
```

```follow
thm subs.eq.4(prop p0, set s0, set s1) {
  |- imp(eq(c(s0),c(s1)), imp(subs(p0,s0,s1), p0))
} = {
  rw12(eq(c(s0),c(s1)), subs(p0,s0,s1), p0, exist(hs0,and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1)))), forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0)))))
  eq.exist(s0, s1, hs0)
  subs.def.2(p0, s0, s1, hs0)
  com12i(exist(hs0,and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1)))), forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0)))), p0)
  syl(forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0)))), imp(exist(hs0,and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1)))),p0), forall(hs0,imp(and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1))),p0)))
  a4.diff(hs0, and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1))), p0)
  a4.1i(hs0, imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))), imp(and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1))),p0))
  gen(hs0, imp(imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))),imp(and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1))),p0)))
  and.impod(imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))), eq(c(hs0),c(s0)), eq(c(hs0),c(s1)), p0)
  com23i(imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))), eq(c(hs0),c(s0)), eq(c(hs0),c(s1)), p0)
  mp(imp(imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0))),imp(eq(c(hs0),c(s1)),imp(eq(c(hs0),c(s0)),p0))), imp(forall(s0,imp(eq(c(s0),c(hs0)),p0)),imp(eq(c(hs0),c(s0)),p0)))
  trans(eq(c(hs0),c(s1)), forall(s0,imp(eq(c(s0),c(hs0)),p0)), imp(eq(c(hs0),c(s0)),p0))
  rw2(forall(s0,imp(eq(c(s0),c(hs0)),p0)), eq(c(hs0),c(s0)), p0, eq(c(s0),c(hs0)))
  eq.com(hs0, s0)
  special.2(imp(eq(c(s0),c(hs0)),p0), s0)
}
```


```follow
thm subs.eq.5(prop p0, set s0, set s1) {
  // 第一组
  |- imp(eq(c(s0),c(s1)), imp(p0, subs(p0,s0,s1)))
  |- imp(eq(c(s0),c(s1)), imp(subs(p0,s0,s1), p0))
  |- imp(eq(c(s0),c(s1)), iff(p0, subs(p0,s0,s1)))
  |- imp(eq(c(s0),c(s1)), iff(subs(p0,s0,s1), p0))

  // 第二组
  |- imp(eq(c(s1),c(s0)), imp(p0, subs(p0,s0,s1)))
  |- imp(eq(c(s1),c(s0)), imp(subs(p0,s0,s1), p0))
  |- imp(eq(c(s1),c(s0)), iff(p0, subs(p0,s0,s1)))
  |- imp(eq(c(s1),c(s0)), iff(subs(p0,s0,s1), p0))
} = {
  syl(eq(c(s1),c(s0)), imp(p0,subs(p0,s0,s1)), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  syl(eq(c(s1),c(s0)), imp(subs(p0,s0,s1),p0), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  syl(eq(c(s1),c(s0)), iff(p0,subs(p0,s0,s1)), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  syl(eq(c(s1),c(s0)), iff(subs(p0,s0,s1),p0), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  iff.introd(eq(c(s0),c(s1)), p0, subs(p0,s0,s1))
  iff.introd(eq(c(s0),c(s1)), subs(p0,s0,s1), p0)
  subs.eq.3(p0, s0, s1)
  subs.eq.4(p0, s0, s1)
}
```

```follow
thm subs.eq.6(prop p0, set s0, set s1) {
  |- imp(eq(c(s0), c(s1)), iff(subs(p0, s0, s1), subs(p0, s1, s0)))
  |- imp(eq(c(s0), c(s1)), iff(subs(p0, s1, s0), subs(p0, s0, s1)))
  |- imp(eq(c(s0), c(s1)), imp(subs(p0, s0, s1), subs(p0, s1, s0)))
  |- imp(eq(c(s0), c(s1)), imp(subs(p0, s1, s0), subs(p0, s0, s1)))
} = {
  iff.comd(eq(c(s0),c(s1)), subs(p0,s1,s0), subs(p0,s0,s1))
  iff.leftd(eq(c(s0),c(s1)), subs(p0,s0,s1), subs(p0,s1,s0))
  iff.rightd(eq(c(s0),c(s1)), subs(p0,s0,s1), subs(p0,s1,s0))
  iff.syld(eq(c(s0),c(s1)), subs(p0,s0,s1), subs(p0,s1,s0), p0)
  subs.eq.5(p0, s0, s1)
  subs.eq.5(p0, s1, s0)
}
```

### `subs` 和 `id` 

```follow
thm subs.id(prop p0, set s0) {
  |- iff(subs(p0, s0, s0), p0)
  |- iff(p0, subs(p0, s0, s0))
  |- imp(subs(p0, s0, s0), p0)
  |- imp(p0, subs(p0, s0, s0))

} = {
  iff.comi(p0, subs(p0,s0,s0))
  iff.lefti(subs(p0,s0,s0), p0)
  iff.righti(subs(p0,s0,s0), p0)
  mp(iff(subs(p0,s0,s0),p0), eq(c(s0),c(s0)))
  subs.eq.5(p0, s0, s0)
  eq.id(s0)
}
```

```follow
thm subs.id.diff.1(prop p0, set s0, set s1) {
  |- iff(subs(subs(p0,s0,s1),s1,s0), subs(p0,s1,s0))
  |- iff(subs(p0,s1,s0), subs(subs(p0,s0,s1),s1,s0))
  |- imp(subs(subs(p0,s0,s1),s1,s0), subs(p0,s1,s0))
  |- imp(subs(p0,s1,s0), subs(subs(p0,s0,s1),s1,s0))
  diff (s0, s1)
} = {
  iff.comi(subs(p0,s1,s0), subs(subs(p0,s0,s1),s1,s0))
  iff.lefti(subs(subs(p0,s0,s1),s1,s0), subs(p0,s1,s0))
  iff.righti(subs(subs(p0,s0,s1),s1,s0), subs(p0,s1,s0))
  iff.syl(subs(subs(p0,s0,s1),s1,s0), subs(p0,s1,s0), subs(subs(p0,s0,s0),s1,s0))
  subs.subs.diff(p0, s0, s1, s0)
  subs.iffsubs.gen(subs(p0,s0,s0), p0, s1, s0)
  subs.id(p0, s0)
}
```

```follow
thm subs.id.diff.2(prop p0, set s0, set s1) {
  |- iff(subs(subs(p0, s0, s1), s1, s0), p0)
  |- iff(p0, subs(subs(p0, s0, s1), s1, s0))
  |- imp(subs(subs(p0, s0, s1), s1, s0), p0)
  |- imp(p0, subs(subs(p0, s0, s1), s1, s0))
  diff (s1, p0) (s1, s0)
} = {
  iff.comi(p0, subs(subs(p0,s0,s1),s1,s0))
  iff.lefti(subs(subs(p0,s0,s1),s1,s0), p0)
  iff.righti(subs(subs(p0,s0,s1),s1,s0), p0)
  iff.syl(subs(subs(p0,s0,s1),s1,s0), p0, subs(p0,s0,s0))
  subs.trans.diff(p0, s0, s1, s0)
  subs.id(p0, s0)
}
```

```follow
thm subs.id.diff.3(prop p0, set s0, set s1) {
  |- iff(subs(p0, s0, s1), p0)
  |- iff(p0, subs(p0, s0, s1))
  |- imp(subs(p0, s0, s1), p0)
  |- imp(p0, subs(p0, s0, s1))
  diff (s0, p0)
} = {
  iff.comi(p0, subs(p0,s0,s1))
  iff.lefti(subs(p0,s0,s1), p0)
  iff.righti(subs(p0,s0,s1), p0)
  iff.introii(subs(p0,s0,s1), p0) syl(subs(p0,s0,s1), p0, exist(s0,p0))
  subs.exist(p0, s0, s1)
  syl(p0, subs(p0,s0,s1), forall(s0,p0))
  subs.forall(p0, s0, s1)
  a5(s0, p0)
}
```

### `subs` 新定义

```follow
thm subs.def.3(prop p0, set s0, set s1) {
  |- iff(subs(p0, s0, s1), forall(s0, imp(eq(c(s0),c(s1)), p0)))
  |- iff(forall(s0, imp(eq(c(s0),c(s1)), p0)), subs(p0, s0, s1))
  |- imp(subs(p0, s0, s1), forall(s0, imp(eq(c(s0),c(s1)), p0)))
  |- imp(forall(s0, imp(eq(c(s0),c(s1)), p0)), subs(p0, s0, s1))
  diff (s0, s1)
} = {
  iff.comi(forall(s0,imp(eq(c(s0),c(s1)),p0)), subs(p0,s0,s1))
  iff.lefti(subs(p0,s0,s1), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  iff.righti(subs(p0,s0,s1), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  iff.syl(subs(p0,s0,s1), forall(s0,imp(eq(c(s0),c(s1)),p0)), forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),p0)))))
  subs.def.2(p0, s0, s1, hs0)
  eq.forall.3(forall(s0,imp(eq(c(s0),c(hs0)),p0)), hs0, forall(s0,imp(eq(c(s0),c(s1)),p0)), s1)
  syl(eq(c(hs0),c(s1)), iff(forall(s0,imp(eq(c(s0),c(hs0)),p0)),forall(s0,imp(eq(c(s0),c(s1)),p0))), forall(s0,iff(imp(eq(c(s0),c(hs0)),p0),imp(eq(c(s0),c(s1)),p0))))
  a4(s0, imp(eq(c(s0),c(hs0)),p0), imp(eq(c(s0),c(s1)),p0))
  gend(s0, iff(imp(eq(c(s0),c(hs0)),p0),imp(eq(c(s0),c(s1)),p0)), eq(c(hs0),c(s1)))
  imp.iffimpd(eq(c(hs0),c(s1)), eq(c(s0),c(hs0)), eq(c(s0),c(s1)), p0, p0)
  iff.idd(eq(c(hs0),c(s1)), p0)
  eq.trans(hs0, s1, s0)
}
```

### `subs` `eq` 继续

```follow
thm subs.eq.7(prop p0, set s0, prop p1, set s1) {
  |- iff(subs(p0, s0, s1), p1)
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, p1) (s0, s1)
} = {
  iff.syl(subs(p0,s0,s1), p1, forall(s0,imp(eq(c(s0),c(s1)),p0)))
  subs.def.3(p0, s0, s1)
  eq.forall.3(p0, s0, p1, s1)
}
```

## `foralleq.foralleq`

```follow
thm foralleq.foralleq.1(set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s0), c(s1))), forall(s2, eq(c(s2), c(s1))))
  diff (s0, s1) (s2, s1)
} = {
  syl(forall(s0,eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s1))), forall(hs0,eq(c(hs0),c(s1))))
  eq.forall.4(eq(c(s0),c(s1)), s0, eq(c(hs0),c(s1)), hs0)
  eq.trans(s0, hs0, s1)
  eq.forall.4(eq(c(hs0),c(s1)), hs0, eq(c(s2),c(s1)), s2)
  eq.trans(hs0, s2, s1)
}
```

```follow
thm foralleq.foralleq.2(set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s0), c(s1))), forall(s2, eq(c(s2), c(s0))))
  diff (s0, s1, s2)
} = {
  mpd(forall(s0,eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s0))), forall(s2,eq(c(s2),c(s1))))
  foralleq.foralleq.1(s0, s1, s2)
  a4.1d(s2, eq(c(s2),c(s1)), eq(c(s2),c(s0)), forall(s0,eq(c(s0),c(s1))))
  syl(forall(s0,eq(c(s0),c(s1))), forall(s2,imp(eq(c(s2),c(s1)),eq(c(s2),c(s0)))), eq(c(s0),c(s1)))
  special.2(eq(c(s0),c(s1)), s0)
  gend(s2, imp(eq(c(s2),c(s1)),eq(c(s2),c(s0))), eq(c(s0),c(s1)))
  eq.trans(s0, s1, s2)
}
```

```follow
thm foralleq.foralleq.3(set s0, set s1, set s2, set s3) {
  |- imp(forall(s0, eq(c(s0), c(s1))), forall(s2, eq(c(s2), c(s3))))
  diff (s0, s1) (s2, s3)
} = {
  syl(forall(s0,eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s3))), forall(hs0,eq(c(hs0),c(s1))))
  foralleq.foralleq.1(s0, s1, hs0)
  syl(forall(hs0,eq(c(hs0),c(s1))), forall(s2,eq(c(s2),c(s3))), forall(s0,eq(c(s0),c(hs0))))
  foralleq.foralleq.2(hs0, s1, s0)
  syl(forall(s0,eq(c(s0),c(hs0))), forall(s2,eq(c(s2),c(s3))), forall(s3,eq(c(s3),c(hs0))))
  foralleq.foralleq.1(s0, hs0, s3)
  foralleq.foralleq.2(s3, hs0, s2)
}
```

```follow
thm foralleq.com(set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s1), c(s2))), forall(s0, eq(c(s2), c(s1))))
} = {
  a4.1i(s0, eq(c(s1),c(s2)), eq(c(s2),c(s1)))
  gen(s0, imp(eq(c(s1),c(s2)),eq(c(s2),c(s1))))
  eq.com(s1, s2)
}
```

```follow
thm foralleq.foralleq(set s0, set s1, set s2, set s3) {
  |- imp(forall(s0, eq(c(s0), c(s1))), forall(s2, eq(c(s2), c(s3))))
  |- imp(forall(s0, eq(c(s1), c(s0))), forall(s2, eq(c(s2), c(s3))))
  |- imp(forall(s0, eq(c(s0), c(s1))), forall(s2, eq(c(s3), c(s2))))
  |- imp(forall(s0, eq(c(s1), c(s0))), forall(s2, eq(c(s3), c(s2))))
  diff (s0, s1) (s2, s3)
} = {
  syl(forall(s0,eq(c(s1),c(s0))), forall(s2,eq(c(s2),c(s3))), forall(s0,eq(c(s0),c(s1))))
  syl(forall(s0,eq(c(s1),c(s0))), forall(s2,eq(c(s3),c(s2))), forall(s0,eq(c(s0),c(s1))))
  foralleq.com(s0, s1, s0)
  syl(forall(s0,eq(c(s0),c(s1))), forall(s2,eq(c(s3),c(s2))), forall(s2,eq(c(s2),c(s3))))
  foralleq.com(s2, s2, s3)
  foralleq.foralleq.3(s0, s1, s2, s3)
}
```


## `foralleq`

```follow
thm foralleq.a5.1(prop p0, set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s0),c(s1))), imp(p0, forall(s2, p0)))
  diff (s0, s1)
} = {
  syl(forall(s0,eq(c(s0),c(s1))), imp(p0,forall(s2,p0)), forall(s2,eq(c(s2),c(hs0))))
  foralleq.foralleq(s0, s1, s2, hs0)
  syld(forall(s2,eq(c(s2),c(hs0))), p0, forall(s2,p0), forall(s2,imp(eq(c(s2),c(hs0)),p0)))
  com12i(forall(s2,eq(c(s2),c(hs0))), forall(s2,imp(eq(c(s2),c(hs0)),p0)), forall(s2,p0))
  a4(s2, eq(c(s2),c(hs0)), p0)
  syl(forall(s2,eq(c(s2),c(hs0))), imp(p0,forall(s2,imp(eq(c(s2),c(hs0)),p0))), eq(c(s2),c(hs0)))
  a12(p0, s2, hs0)
  special.2(eq(c(s2),c(hs0)), s2)
}
```

```follow
thm foralleq.a5.2(prop p0, set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s0),c(s1))), iff(p0, forall(s2, p0)))
  |- imp(forall(s0, eq(c(s0),c(s1))), iff(forall(s2, p0), p0))
  |- imp(forall(s0, eq(c(s0),c(s1))), imp(p0, forall(s2, p0)))
  |- imp(forall(s0, eq(c(s0),c(s1))), imp(forall(s2, p0), p0))
  diff (s0, s1)
} = {
  iff.introd(forall(s0,eq(c(s0),c(s1))), p0, forall(s2,p0))
  iff.introd(forall(s0,eq(c(s0),c(s1))), forall(s2,p0), p0)
  foralleq.a5.1(p0, s0, s1, s2)
  a1i(forall(s0,eq(c(s0),c(s1))), imp(forall(s2,p0),p0))
  special.2(p0, s2)
}
```

```follow
thm foralleq.a5.3(prop p0, set s0, set s1) {
  |- imp(forall(s0, eq(c(s0), c(s1))), imp(forall(s0, p0), forall(s1, p0)))
  diff (s0, s1)
} = {
  rw2(forall(s0,eq(c(s0),c(s1))), forall(s0,p0), forall(s1,p0), p0)
  foralleq.a5.2(p0, s0, s1, s1)
  special.2(p0, s0)
}
```

```follow
thm foralleq.a5.4(prop p0, set s0, set s1) {
  |- imp(forall(s0, eq(c(s0), c(s1))), imp(forall(s1, p0), forall(s0, p0)))
  diff (s0, s1)
} = {
  syl(forall(s0,eq(c(s0),c(s1))), imp(forall(s1,p0),forall(s0,p0)), forall(s1,eq(c(s1),c(s0))))
  foralleq.a5.3(p0, s1, s0)
  foralleq.foralleq(s0, s1, s1, s0)
}
```

```follow
thm foralleq.a5.5(prop p0, set s0, set s1) {
  |- imp(forall(s0, eq(c(s0), c(s1))), iff(forall(s0, p0), forall(s1, p0)))
  |- imp(forall(s0, eq(c(s0), c(s1))), iff(forall(s1, p0), forall(s0, p0)))
  |- imp(forall(s0, eq(c(s0), c(s1))), imp(forall(s0, p0), forall(s1, p0)))
  |- imp(forall(s0, eq(c(s0), c(s1))), imp(forall(s1, p0), forall(s0, p0)))
  diff (s0, s1)
} = {
  iff.introd(forall(s0,eq(c(s0),c(s1))), forall(s0,p0), forall(s1,p0))
  iff.introd(forall(s0,eq(c(s0),c(s1))), forall(s1,p0), forall(s0,p0))
  foralleq.a5.3(p0, s0, s1)
  foralleq.a5.4(p0, s0, s1)
}
```


```follow
thm foralleq.a5(prop p0, set s0, set s1) {
  // 第一组
  |- imp(forall(s0, eq(c(s0), c(s1))), iff(forall(s0, p0), forall(s1, p0)))
  |- imp(forall(s0, eq(c(s0), c(s1))), iff(forall(s1, p0), forall(s0, p0)))
  |- imp(forall(s0, eq(c(s0), c(s1))), imp(forall(s0, p0), forall(s1, p0)))
  |- imp(forall(s0, eq(c(s0), c(s1))), imp(forall(s1, p0), forall(s0, p0)))

  // 第二组
  |- imp(forall(s0, eq(c(s0),c(s1))), iff(p0, forall(s1, p0)))
  |- imp(forall(s0, eq(c(s0),c(s1))), iff(forall(s1, p0), p0))
  |- imp(forall(s0, eq(c(s0),c(s1))), imp(p0, forall(s1, p0)))
  |- imp(forall(s0, eq(c(s0),c(s1))), imp(forall(s1, p0), p0))
  diff (s0, s1)
} = {
  foralleq.a5.2(p0, s0, s1, s1)
  foralleq.a5.5(p0, s0, s1)
}
```

## `subs` 与 `notfree` 

```follow
thm nf.gend(set s0, prop p0, prop p1) {
  |- imp(p1, forall(s0, p0))
  -| imp(p1, p0)
  -| nf(s0, p1)
} = {
  syl(p1, forall(s0,p0), forall(s0,p1))
  a4.1i(s0, p1, p0)
  gen(s0, imp(p1,p0))
  nf.a5i(p1, s0)
}
```

```follow
thm nf.subs.2subs.gend(prop p0, prop p1, prop p2, set s1, set s2) {
  |- imp(p0, imp(subs(p1, s1, s2), subs(p2, s1, s2)))
  -| imp(p0, imp(p1, p2))
  -| nf(s1, p0)
} = {
  syl(p0, imp(subs(p1,s1,s2),subs(p2,s1,s2)), forall(s1,imp(p1,p2)))
  subs.2subs.2(p1, p2, s1, s2)
  nf.gend(s1, imp(p1,p2), p0)
}
```

```follow
thm nf.subs.iffsubs.gend(prop p0, prop p1, prop p2, set s1, set s2) {
  |- imp(p0, iff(subs(p1, s1, s2), subs(p2, s1, s2)))
  -| imp(p0, iff(p1, p2))
  -| nf(s1, p0)
} = {
  syl(p0, iff(subs(p1,s1,s2),subs(p2,s1,s2)), forall(s1,iff(p1,p2)))
  subs.iffsubs.1(p1, p2, s1, s2)
  nf.gend(s1, iff(p1,p2), p0)
}
```

```follow
thm nf.subs.id(prop p0, set s0, set s1) {
  |- imp(nf(s0,p0), iff(subs(p0,s0,s1), p0))
  |- imp(nf(s0,p0), iff(p0, subs(p0,s0,s1)))
  |- imp(nf(s0,p0), imp(subs(p0,s0,s1), p0))
  |- imp(nf(s0,p0), imp(p0, subs(p0,s0,s1)))
} = {
  iff.comd(nf(s0,p0), p0, subs(p0,s0,s1))
  iff.leftd(nf(s0,p0), subs(p0,s0,s1), p0)
  iff.rightd(nf(s0,p0), subs(p0,s0,s1), p0)
  iff.introd(nf(s0,p0), subs(p0,s0,s1), p0)
  rw2(nf(s0,p0), subs(p0,s0,s1), p0, exist(s0,p0))
  subs.exist(p0, s0, s1)
  rw3(nf(s0,p0), p0, subs(p0,s0,s1), forall(s0,p0))
  subs.forall(p0, s0, s1)
  nf.a5(p0, s0)
}
```

## `subs` 等价定义

```follow
thm nf.eq.exist(set s0, set s1, prop p0, prop p1) {
  |- iff(exist(s0, and(eq(c(s0), c(s1)), p0)), p1)
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  -| nf(s0, p1)
  diff (s0, s1)
} = {
  iff.syl(exist(s0,and(eq(c(s0),c(s1)),p0)), p1, exist(s0, and(eq(c(s0), c(s1)), p1)))
  mp(iff(exist(s0,and(eq(c(s0),c(s1)),p0)),exist(s0,and(eq(c(s0),c(s1)),p1))), forall(s0,iff(and(eq(c(s0),c(s1)),p0),and(eq(c(s0),c(s1)),p1))))
  a4(s0, and(eq(c(s0),c(s1)),p0), and(eq(c(s0),c(s1)),p1))
  gen(s0, iff(and(eq(c(s0),c(s1)),p0),and(eq(c(s0),c(s1)),p1)))
  and.iffand2i(eq(c(s0),c(s1)), p0, p1)
  iff.and.mp(exist(s0,and(eq(c(s0),c(s1)),p1)), p1, exist(s0,eq(c(s0),c(s1))))
  a6.2(s0, s1)
  nf.a4i(s0, p1, eq(c(s0),c(s1)))
}
```

```follow
thm subs.def.4.lemma(prop p0, set s0, set s1) {
  |- iff(exist(s0, and(eq(c(s0),c(s1)), p0)), forall(s0, imp(eq(c(s0), c(s1)), p0)))
  |- iff(forall(s0, imp(eq(c(s0), c(s1)), p0)),exist(s0, and(eq(c(s0),c(s1)), p0)))
  |- imp(exist(s0, and(eq(c(s0),c(s1)), p0)), forall(s0, imp(eq(c(s0), c(s1)), p0)))
  |- imp(forall(s0, imp(eq(c(s0), c(s1)), p0)),exist(s0, and(eq(c(s0),c(s1)), p0)))
  diff (s0, s1)
} = {
  iff.comi(forall(s0,imp(eq(c(s0),c(s1)),p0)), exist(s0,and(eq(c(s0),c(s1)),p0)))
  iff.lefti(exist(s0,and(eq(c(s0),c(s1)),p0)), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  iff.righti(exist(s0,and(eq(c(s0),c(s1)),p0)), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  nf.eq.exist(s0, s1, p0, forall(s0,imp(eq(c(s0),c(s1)),p0)))
  nf.forall.1(s0, imp(eq(c(s0),c(s1)),p0))
  iff.introd(eq(c(s0),c(s1)), p0, forall(s0,imp(eq(c(s0),c(s1)),p0)))
  a12(p0, s0, s1)
  com12i(eq(c(s0),c(s1)), forall(s0,imp(eq(c(s0),c(s1)),p0)), p0)
  special(imp(eq(c(s0),c(s1)),p0), s0)
}
```

```follow
thm subs.def.4(prop p0, set s0, set s1) {
  |- iff(subs(p0,s0,s1), exist(s0,and(eq(c(s0),c(s1)), p0)))
  |- iff(exist(s0,and(eq(c(s0),c(s1)), p0)), subs(p0,s0,s1))
  |- imp(subs(p0,s0,s1), exist(s0,and(eq(c(s0),c(s1)), p0)))
  |- imp(exist(s0,and(eq(c(s0),c(s1)), p0)), subs(p0,s0,s1))
  diff (s0,s1)
} = {
  iff.comi(exist(s0,and(eq(c(s0),c(s1)),p0)), subs(p0,s0,s1))
  iff.lefti(subs(p0,s0,s1), exist(s0,and(eq(c(s0),c(s1)),p0)))
  iff.righti(subs(p0,s0,s1), exist(s0,and(eq(c(s0),c(s1)),p0)))
  iff.syl(subs(p0,s0,s1), exist(s0,and(eq(c(s0),c(s1)),p0)), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  subs.def.3(p0, s0, s1)
  subs.def.4.lemma(p0, s0, s1)
}
```

```follow
thm subs.def.5(prop p0, set s0, set s1, set s2) {
  |- iff(subs(p0,s0,s1),exist(s2,and(eq(c(s2),c(s1)), exist(s0,and(eq(c(s0),c(s2)),p0)))))
  |- iff(exist(s2,and(eq(c(s2),c(s1)), exist(s0,and(eq(c(s0),c(s2)),p0)))), subs(p0,s0,s1))
  |- imp(subs(p0,s0,s1),exist(s2,and(eq(c(s2),c(s1)), exist(s0,and(eq(c(s0),c(s2)),p0)))))
  |- imp(exist(s2,and(eq(c(s2),c(s1)), exist(s0,and(eq(c(s0),c(s2)),p0)))), subs(p0,s0,s1))
  diff (s0,s2) (s1,s2) (s2,p0)
} = {
  iff.comi(exist(s2,and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0)))), subs(p0,s0,s1))
  iff.lefti(subs(p0,s0,s1), exist(s2,and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0)))))
  iff.righti(subs(p0,s0,s1), exist(s2,and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0)))))
  iff.syl(subs(p0,s0,s1), exist(s2,and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0)))), forall(s2,imp(eq(c(s2),c(s1)),forall(s0,imp(eq(c(s0),c(s2)),p0)))))
  subs.def.1(p0, s0, s1, s2)
  iff.syl(forall(s2,imp(eq(c(s2),c(s1)),forall(s0,imp(eq(c(s0),c(s2)),p0)))), exist(s2,and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0)))), exist(s2,and(eq(c(s2),c(s1)),forall(s0,imp(eq(c(s0),c(s2)),p0)))))
  subs.def.4.lemma(forall(s0,imp(eq(c(s0),c(s2)),p0)), s2, s1)
  mp(iff(exist(s2,and(eq(c(s2),c(s1)),forall(s0,imp(eq(c(s0),c(s2)),p0)))),exist(s2,and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0))))), forall(s2,iff(and(eq(c(s2),c(s1)),forall(s0,imp(eq(c(s0),c(s2)),p0))),and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0))))))
  a4(s2, and(eq(c(s2),c(s1)),forall(s0,imp(eq(c(s0),c(s2)),p0))), and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0))))
  gen(s2, iff(and(eq(c(s2),c(s1)),forall(s0,imp(eq(c(s0),c(s2)),p0))),and(eq(c(s2),c(s1)),exist(s0,and(eq(c(s0),c(s2)),p0)))))
  and.iffandii(eq(c(s2),c(s1)), forall(s0,imp(eq(c(s0),c(s2)),p0)), eq(c(s2),c(s1)), exist(s0,and(eq(c(s0),c(s2)),p0)))
  iff.id(eq(c(s2),c(s1)))
  subs.def.4.lemma(p0, s0, s2)
}
```

```follow
thm subs.not(prop p0, set s0, set s1) {
  |- iff(subs(not(p0),s0,s1), not(subs(p0,s0,s1)))
  |- iff(not(subs(p0,s0,s1)), subs(not(p0),s0,s1))
  |- imp(subs(not(p0),s0,s1), not(subs(p0,s0,s1)))
  |- imp(not(subs(p0,s0,s1)), subs(not(p0),s0,s1))
} = {
  iff.comi(not(subs(p0,s0,s1)), subs(not(p0),s0,s1))
  iff.lefti(subs(not(p0),s0,s1), not(subs(p0,s0,s1)))
  iff.righti(subs(not(p0),s0,s1), not(subs(p0,s0,s1)))
  iff.syl(subs(not(p0),s0,s1), not(subs(p0,s0,s1)), forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),not(p0))))))
  subs.def.1(not(p0), s0, s1, hs0)
  iff.syl(forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),not(p0))))), not(subs(p0,s0,s1)), not(exist(hs0,and(eq(c(hs0),c(s1)),exist(s0,and(eq(c(s0),c(hs0)),p0))))))
  mp(iff(not(exist(hs0,and(eq(c(hs0),c(s1)),exist(s0,and(eq(c(s0),c(hs0)),p0))))),not(subs(p0,s0,s1))), iff(subs(p0,s0,s1),exist(hs0,and(eq(c(hs0),c(s1)),exist(s0,and(eq(c(s0),c(hs0)),p0))))))
  iff.con(subs(p0,s0,s1), exist(hs0,and(eq(c(hs0),c(s1)),exist(s0,and(eq(c(s0),c(hs0)),p0)))))
  subs.def.5(p0, s0, s1, hs0)
  iff.syl(forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),not(p0))))), not(exist(hs0,and(eq(c(hs0),c(s1)),exist(s0,and(eq(c(s0),c(hs0)),p0))))), forall(hs0,imp(eq(c(hs0),c(s1)),not(exist(s0,and(eq(c(s0),c(hs0)),p0))))))
  exist.and.2(hs0, eq(c(hs0),c(s1)), exist(s0,and(eq(c(s0),c(hs0)),p0)))
  mp(iff(forall(hs0,imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),not(p0))))),forall(hs0,imp(eq(c(hs0),c(s1)),not(exist(s0,and(eq(c(s0),c(hs0)),p0)))))), forall(hs0,iff(imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),not(p0)))),imp(eq(c(hs0),c(s1)),not(exist(s0,and(eq(c(s0),c(hs0)),p0)))))))
  a4(hs0, imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),not(p0)))), imp(eq(c(hs0),c(s1)),not(exist(s0,and(eq(c(s0),c(hs0)),p0)))))
  gen(hs0, iff(imp(eq(c(hs0),c(s1)),forall(s0,imp(eq(c(s0),c(hs0)),not(p0)))),imp(eq(c(hs0),c(s1)),not(exist(s0,and(eq(c(s0),c(hs0)),p0))))))
  imp.iffimpii(eq(c(hs0),c(s1)), eq(c(hs0),c(s1)), forall(s0,imp(eq(c(s0),c(hs0)),not(p0))), not(exist(s0,and(eq(c(s0),c(hs0)),p0))))
  iff.id(eq(c(hs0),c(s1)))
  exist.and.2(s0, eq(c(s0),c(hs0)), p0)
}
```

```follow
thm nf.subs(prop p0, set s0, set s1) {
  |- nf(s0,subs(p0,s0,s1))
  diff (s0,s1)
} = {
  nf.rw(s0, subs(p0,s0,s1), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  subs.def.3(p0, s0, s1)
  nf.forall.1(s0, imp(eq(c(s0),c(s1)),p0))
}
```

```follow
thm subs.a5(prop p0, set s0, set s1) {
  |- imp(subs(p0,s0,s1),forall(s0,subs(p0,s0,s1)))
  diff (s0,s1)
} = {
  nf.a5i(subs(p0,s0,s1), s0)
  nf.subs(p0, s0, s1)
}
```

```follow
thm nf.def.6(set s0, prop p0) {
  |- iff(nf(s0,p0), forall(s0, imp(p0,forall(s0,p0))))
  |- iff(forall(s0, imp(p0,forall(s0,p0))), nf(s0,p0))
  |- imp(nf(s0,p0), forall(s0, imp(p0,forall(s0,p0))))
  |- imp(forall(s0, imp(p0,forall(s0,p0))), nf(s0,p0))
} = {
  iff.comi(forall(s0,imp(p0,forall(s0,p0))), nf(s0,p0))
  iff.lefti(nf(s0,p0), forall(s0,imp(p0,forall(s0,p0))))
  iff.righti(nf(s0,p0), forall(s0,imp(p0,forall(s0,p0))))
  iff.syl(nf(s0,p0), forall(s0,imp(p0,forall(s0,p0))), imp(exist(s0,p0),forall(s0,p0)))
  nf.def.5(s0, p0)
  nf.a4i(s0, forall(s0,p0), p0)
  nf.forall.1(s0, p0)
}
```

```follow
thm nf.def.7(set s0, prop p0) {
  |- iff(nf(s0,p0), forall(s0, imp(exist(s0,p0),p0)))
  |- iff(forall(s0, imp(exist(s0,p0),p0)), nf(s0,p0))
  |- imp(nf(s0,p0), forall(s0, imp(exist(s0,p0),p0)))
  |- imp(forall(s0, imp(exist(s0,p0),p0)), nf(s0,p0))
} = {
  iff.comi(forall(s0,imp(exist(s0,p0),p0)), nf(s0,p0))
  iff.lefti(nf(s0,p0), forall(s0,imp(exist(s0,p0),p0)))
  iff.righti(nf(s0,p0), forall(s0,imp(exist(s0,p0),p0)))
  iff.syl(nf(s0,p0), forall(s0,imp(exist(s0,p0),p0)), imp(exist(s0,p0),forall(s0,p0)))
  nf.def.5(s0, p0)
  nf.a4i(s0, exist(s0,p0), p0)
  nf.exist.1(s0, p0)
}
```

```follow
thm nf.def.7gen(set s0, prop p0) {
  |- nf(s0,p0)
  -| imp(exist(s0,p0),p0)
} = {
  mp(nf(s0,p0), forall(s0,imp(exist(s0,p0),p0)))
  nf.def.7(s0, p0)
  gen(s0, imp(exist(s0,p0),p0))
}
```

```follow
thm nf.def.6gen(set s0, prop p0) {
  |- nf(s0,p0)
  -| imp(p0,forall(s0,p0))
} = {
  mp(nf(s0,p0), forall(s0,imp(p0,forall(s0,p0))))
  nf.def.6(s0, p0)
  gen(s0, imp(p0,forall(s0,p0)))
}
```

```follow
thm nf.def.6d(set s0, prop p0, prop p1) {
  |- imp(p1, nf(s0,p0))
  -| imp(p1, imp(p0,forall(s0,p0)))
  -| nf(s0,p1)
} = {
  syl(p1, nf(s0,p0), forall(s0,imp(p0,forall(s0,p0))))
  nf.def.6(s0, p0)
  nf.gend(s0, imp(p0,forall(s0,p0)), p1)
}
```

```follow
thm nf.def.6di(set s0, prop p0) {
  |- nf(s0,p0)
  -| imp(p0,nf(s0,p0))
} = {
  mp(nf(s0,p0), forall(s0,imp(p0,forall(s0,p0))))
  nf.def.6(s0, p0)
  gen(s0, imp(p0,forall(s0,p0)))
  iid(p0, forall(s0,p0))
  syl(p0, imp(p0,forall(s0,p0)), nf(s0,p0))
  nf.a5(p0, s0)
}
```

```follow
thm nf.con3(set s0, prop p0) {
  |- imp(forall(s0,imp(p0,forall(s0,p0))),imp(not(p0),forall(s0,not(p0))))
} = {
  syl(forall(s0,imp(p0,forall(s0,p0))), imp(not(p0),forall(s0,not(p0))), nf(s0,p0))
  nf.def.6(s0, p0)
  syl(nf(s0,p0), imp(not(p0),forall(s0,not(p0))), nf(s0,not(p0)))
  nf.not(s0, p0)
  syl(nf(s0,not(p0)), imp(not(p0),forall(s0,not(p0))), forall(s0,imp(not(p0),forall(s0,not(p0)))))
  nf.def.6(s0, not(p0))
  special(imp(not(p0),forall(s0,not(p0))), s0)
}
```

## `subs.2subs`

```follow
thm subs.2subs.3(prop p0, prop p1, set s0, set s1) {
  |- imp(imp(subs(p0,s0,s1), subs(p1,s0,s1)), subs(imp(p0,p1), s0, s1))
} = {
  joinii(subs(p0,s0,s1), subs(p1,s0,s1), subs(imp(p0,p1),s0,s1))
  subs.2subs.gen(p1, imp(p0,p1), s0, s1)
  a1(p1, p0)
  syl(not(subs(p0,s0,s1)), subs(imp(p0,p1),s0,s1), subs(not(p0),s0,s1))
  subs.not(p0, s0, s1)
  subs.2subs.gen(not(p0), imp(p0,p1), s0, s1)
  cont(p0, p1)
}
```

```follow
thm subs.2subs.4(prop p0, prop p1, set s0, set s1) {
  |- iff(subs(imp(p0,p1), s0, s1), imp(subs(p0,s0,s1), subs(p1,s0,s1)))
  |- iff(imp(subs(p0,s0,s1), subs(p1,s0,s1)), subs(imp(p0,p1), s0, s1))
  |- imp(subs(imp(p0,p1), s0, s1), imp(subs(p0,s0,s1), subs(p1,s0,s1)))
  |- imp(imp(subs(p0,s0,s1), subs(p1,s0,s1)), subs(imp(p0,p1), s0, s1))
} = {
  iff.introii(subs(imp(p0,p1),s0,s1), imp(subs(p0,s0,s1),subs(p1,s0,s1)))
  iff.introii(imp(subs(p0,s0,s1),subs(p1,s0,s1)), subs(imp(p0,p1),s0,s1))
  subs.2subs.3(p0, p1, s0, s1)
  subs.2subs.1(p0, p1, s0, s1)
}
```

```follow
thm ac7(prop p0, set s0) {
  |- imp(exist(s0,forall(s0,p0)),p0)
} = {
  special.2d(p0, s0, exist(s0,forall(s0,p0)))
  a10(s0, p0)
}
```

```follow
thm nf.forall.2(prop p0, set s0, set s1) {
  |- nf(s0, forall(s1, p0))
  -| nf(s0, p0)
} = {
  nf.def.6gen(s0, forall(s1,p0))
  syl(forall(s1,p0), forall(s0,forall(s1,p0)), forall(s1,forall(s0,p0)))
  a11(p0, s1, s0)
  a4.1i(s1, p0, forall(s0,p0))
  gen(s1, imp(p0,forall(s0,p0)))
  nf.a5.1i(p0, s0)
}
```

```follow
thm nf.exist.2(prop p0, set s0, set s1) {
  |- nf(s0, exist(s1, p0))
  -| nf(s0, p0)
} = {
  nf.rw(s0, exist(s1,p0), not(forall(s1,not(p0))))
  exist.def.2(s1, p0)
  nf.noti(s0, forall(s1,not(p0)))
  nf.forall.2(not(p0), s0, s1)
  nf.noti(s0, p0)
}
```

```follow
thm nf.nf.2(prop p0, set s0, set s1) {
  |- nf(s0, nf(s1, p0))
  -| nf(s0, p0)
} = {
  nf.rw(s0, nf(s1,p0), forall(s1,imp(p0,forall(s1,p0))))
  nf.def.6(s1, p0)
  nf.forall.2(imp(p0,forall(s1,p0)), s0, s1)
  nf.impii(s0, p0, forall(s1,p0))
  nf.forall.2(p0, s0, s1)
}
```

```follow
thm ea.2ae(prop p0, set s0, set s1) {
  |- imp(exist(s0,forall(s1,p0)), forall(s1,exist(s0,p0)))
} = {
  nf.gend(s1, exist(s0,p0), exist(s0,forall(s1,p0)))
  a4.aee.1i(s0, forall(s1,p0), p0)
  gen(s0, imp(forall(s1,p0),p0))
  special(p0, s1)
  nf.exist.2(forall(s1,p0), s1, s0)
  nf.forall.1(s1, p0)
}
```