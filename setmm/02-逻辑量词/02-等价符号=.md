
# 等价符号 `=` 

### Specialization 特例（从 `forall` 推导出特例）

```follow
thm forall.special.1(set s0, set s1, prop p0, prop p1) {
  |- imp(forall(s0, p0), p1)
  -| imp(not(p1), forall(s0, not(p1)))
  -| imp(eq(c(s0), c(s1)), imp(p0, p1))
  diff (s0, s1)
} = {
  mp(imp(forall(s0,p0),p1), exist(s0,eq(c(s0),c(s1))))
  a6.1(s0, s1)
  rw3(exist(s0,eq(c(s0),c(s1))), forall(s0,p0), p1, not(forall(s0,not(p1))))
  con2i(forall(s0,not(p1)), p1)
  rw3(exist(s0,eq(c(s0),c(s1))), forall(s0,p0), not(forall(s0,not(p1))), exist(s0,p1))
  exist.def(s0, p1)
  syl(exist(s0,eq(c(s0),c(s1))), imp(forall(s0,p0),exist(s0,p1)), exist(s0,imp(p0,p1)))
  a4.eae(s0, p0, p1)
  a4.aee.1i(s0, eq(c(s0),c(s1)), imp(p0,p1))
  gen(s0, imp(eq(c(s0),c(s1)),imp(p0,p1)))
}
```

```follow
thm forall.special.2(set s0, set s1, prop p0, prop p1) {
  |- imp(forall(s0, p0), p1)
  -| imp(eq(c(s0), c(s1)), imp(p0, p1))
  diff (s0, p1) (s0, s1)
} = {
  forall.special.1(s0, s1, p0, p1)
  a5.1(s0, not(p1))
}
```

```follow
thm forall.special.3(set s0, prop p1) {
  |- imp(forall(s0, p1), p1)
  -| imp(not(p1), forall(s0, not(p1)))
} = {
  forall.special.1(s0, hs0, p1, p1)
  idd(eq(c(s0),c(hs0)), p1)
}
```

```follow
thm forall.special.4(set s0, prop p1) {
  |- imp(forall(s0, p1), p1)
  -| not(p1)
} = {
  forall.special.3(s0, p1)
  a1i(not(p1), forall(s0,not(p1)))
  gen.1(s0, not(p1))
}
```

## `exist.mp`

```follow
thm exist.mp(set s0, prop p1, prop p2) {
  |- p1
  -| imp(p2, p1)
  -| exist(s0, p2)
  diff (s0, p1)
} = {
  mp(p1, exist(s0,p1))
  a5(s0, p1)
  mp(exist(s0,p1), exist(s0,p2))
  a4.aee.1i(s0, p2, p1)
  gen(s0, imp(p2,p1))
}
```

## 公理 `a7` 

公理`a7`涉及等号的有限的传递性。等号两边需要是`set`，并且穿入参数需要有两个 `diff` 限制。

```follow
axiom a7.1(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(eq(c(s0), c(s2)), eq(c(s1), c(s2))))
  diff (s0, s1) (s0, s2)
}
```

```follow
axiom a7.2(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(eq(c(s0), c(s2)), eq(c(s1), c(s2))))
  diff (s0, s1) (s1, s2)
}
```

```follow
// 有限的eq.id，只涉及set，不涉及class
thm eq.id(set s0) {
  |- eq(c(s0), c(s0))
} = {
  mp(eq(c(s0),c(s0)), exist(hs0,eq(c(s0),c(s0))))
  a5(hs0, eq(c(s0),c(s0)))
  mp(exist(hs0,eq(c(s0),c(s0))), exist(hs0,eq(c(hs0),c(s0))))
  a6.1(hs0, s0)
  a4.aee.1i(hs0, eq(c(hs0),c(s0)), eq(c(s0),c(s0)))
  gen(hs0, imp(eq(c(hs0),c(s0)),eq(c(s0),c(s0))))
  iid(eq(c(hs0),c(s0)), eq(c(s0),c(s0)))
  a7.1(hs0, s0, s0)
}
```

```follow
thm eq.com.1(set s0, set s1) {
  |- imp(eq(c(s0), c(s1)), eq(c(s1), c(s0)))
  diff (s0, s1)
} = {
  mp(imp(eq(c(s0),c(s1)),eq(c(s1),c(s0))), eq(c(s0),c(s0)))
  eq.id(s0)
  com12i(eq(c(s0),c(s0)), eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  a7.2(s0, s1, s0)
}
```

```follow
thm a6.2(set s0, set s1) {
  |- not(forall(s0, not(eq(c(s0), c(s1)))))
  |- exist(s0, eq(c(s0), c(s1)))
  |- not(forall(s0, not(eq(c(s1), c(s0)))))
  |- exist(s0, eq(c(s1), c(s0)))
  diff (s0, s1)
} = {
  mp(not(forall(s0,not(eq(c(s1),c(s0))))), exist(s0,eq(c(s1),c(s0))))
  exist.def(s0, eq(c(s1),c(s0)))
  mp(exist(s0,eq(c(s1),c(s0))), exist(s0,eq(c(s0),c(s1))))
  a6.1(s0, s1)
  a4.aee.1i(s0, eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  gen(s0, imp(eq(c(s0),c(s1)),eq(c(s1),c(s0))))
  eq.com.1(s0, s1)
}
```

```follow
// 因为接受隐变量和公理a6，所以接受无限制的a7
thm a7(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(eq(c(s0), c(s2)), eq(c(s1), c(s2))))
} = {
  mp(imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2)))), exist(hs0,imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2))))))
  a5(hs0, imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2)))))
  mp(exist(hs0,imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2))))), exist(hs0,eq(c(s0),c(hs0))))
  a6.2(hs0, s0)
  a4.aee.1i(hs0, eq(c(s0),c(hs0)), imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2)))))
  gen(hs0, imp(eq(c(s0),c(hs0)),imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2))))))
  syld(eq(c(s0),c(hs0)), eq(c(s0),c(s1)), imp(eq(c(s0),c(s2)),eq(c(s1),c(s2))), eq(c(hs0),c(s1)))
  a7.2(s0, hs0, s1)
  com23i(eq(c(s0),c(hs0)), eq(c(hs0),c(s1)), eq(c(s0),c(s2)), eq(c(s1),c(s2)))
  syld(eq(c(s0),c(hs0)), eq(c(s0),c(s2)), imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2))), eq(c(hs0),c(s2)))
  a7.2(s0, hs0, s2)
  a1i(eq(c(s0),c(hs0)), imp(eq(c(hs0),c(s2)),imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2)))))
  com12i(eq(c(hs0),c(s2)), eq(c(hs0),c(s1)), eq(c(s1),c(s2)))
  a7.1(hs0, s1, s2)
}
```

```follow
thm eq.com(set s0, set s1) {
  |- iff(eq(c(s0), c(s1)), eq(c(s1), c(s0)))
  |- imp(eq(c(s0), c(s1)), eq(c(s1), c(s0)))
} = {
  iff.introii(eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  mp(imp(eq(c(s0),c(s1)),eq(c(s1),c(s0))), eq(c(s0),c(s0)))
  eq.id(s0)
  com12i(eq(c(s0),c(s0)), eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  a7(s0, s1, s0)
  mp(imp(eq(c(s1),c(s0)),eq(c(s0),c(s1))), eq(c(s1),c(s1)))
  eq.id(s1)
  com12i(eq(c(s1),c(s1)), eq(c(s1),c(s0)), eq(c(s0),c(s1)))
  a7(s1, s0, s1)
}
```

### `eq.trans`

```follow
// 终结 eq.trans 的讨论 
// 遗憾的是这个定理还不支持 class 
thm eq.trans(set s0, set s1, set s2) {
  // 第一组
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s0),c(s2)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s0),c(s2)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s0)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s0)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s1),c(s2)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s1),c(s2)), eq(c(s2),c(s0))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s1)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s1)), eq(c(s2),c(s0))))

  // 第二组
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s0),c(s2)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s0),c(s2)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s0)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s0)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s1),c(s2)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s1),c(s2)), eq(c(s2),c(s0))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s1)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s1)), eq(c(s2),c(s0))))
} = {
  iff.introd(eq(c(s0),c(s1)), eq(c(s0),c(s2)), eq(c(s1),c(s2)))
  iff.introd(eq(c(s0),c(s1)), eq(c(s0),c(s2)), eq(c(s2),c(s1)))
  iff.introd(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s1),c(s2)))
  iff.introd(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s2),c(s1)))
  iff.introd(eq(c(s0),c(s1)), eq(c(s1),c(s2)), eq(c(s0),c(s2)))
  iff.introd(eq(c(s0),c(s1)), eq(c(s1),c(s2)), eq(c(s2),c(s0)))
  iff.introd(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s0),c(s2)))
  iff.introd(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s2),c(s0)))
  rw3(eq(c(s0),c(s1)), eq(c(s0),c(s2)), eq(c(s2),c(s1)), eq(c(s1),c(s2)))
  eq.com(s1, s2)
  rw2(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s1),c(s2)), eq(c(s0),c(s2)))
  eq.com(s2, s0)
  rw23(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s2),c(s1)), eq(c(s0),c(s2)), eq(c(s1),c(s2)))
  eq.com(s1, s2)
  eq.com(s2, s0)
  a7(s0, s1, s2)
  syl(eq(c(s0),c(s1)), imp(eq(c(s1),c(s2)),eq(c(s0),c(s2))), eq(c(s1),c(s0)))
  eq.com(s0, s1)
  rw13(eq(c(s0),c(s1)), eq(c(s1),c(s2)), eq(c(s2),c(s0)), eq(c(s1),c(s0)), eq(c(s0),c(s2)))
  eq.com(s0, s1)
  eq.com(s0, s2)
  rw12(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s0),c(s2)), eq(c(s1),c(s0)), eq(c(s1),c(s2)))
  eq.com(s0, s1)
  eq.com(s2, s1)
  rw123(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s2),c(s0)), eq(c(s1),c(s0)), eq(c(s1),c(s2)), eq(c(s0),c(s2)))
  eq.com(s0, s1)
  eq.com(s2, s1)
  eq.com(s0, s2)
  a7(s1, s0, s2)
}
```

### `eq.exist`

```follow
// p1 = p0[s0:s1]
thm eq.exist.1(set s0, set s1, prop p0, prop p1) {
  |- iff(exist(s0, and(eq(c(s0), c(s1)), p0)), p1)
  |- iff(p1, exist(s0, and(eq(c(s0), c(s1)), p0)))
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  iff.comi(p1, exist(s0,and(eq(c(s0),c(s1)),p0)))
  iff.syl(exist(s0,and(eq(c(s0),c(s1)),p0)), p1, exist(s0,and(eq(c(s0),c(s1)),p1)))
  mp(iff(exist(s0,and(eq(c(s0),c(s1)),p0)),exist(s0,and(eq(c(s0),c(s1)),p1))), forall(s0,iff(and(eq(c(s0),c(s1)),p0),and(eq(c(s0),c(s1)),p1))))
  a4(s0, and(eq(c(s0),c(s1)),p0), and(eq(c(s0),c(s1)),p1))
  gen(s0, iff(and(eq(c(s0),c(s1)),p0),and(eq(c(s0),c(s1)),p1)))
  and.iffand2i(eq(c(s0),c(s1)), p0, p1)
  iff.introii(exist(s0,and(eq(c(s0),c(s1)),p1)), p1)
  syl(exist(s0,and(eq(c(s0),c(s1)),p1)), p1, exist(s0,p1))
  a5(s0, p1)
  a4.aee.1i(s0, and(eq(c(s0),c(s1)),p1), p1)
  gen(s0, imp(and(eq(c(s0),c(s1)),p1),p1))
  and.right(eq(c(s0),c(s1)), p1)
  syl(p1, exist(s0,and(eq(c(s0),c(s1)),p1)), and(exist(s0,eq(c(s0),c(s1))),p1))
  a4.diff(s0, eq(c(s0),c(s1)), p1)
  and.introd(p1, exist(s0,eq(c(s0),c(s1))), p1)
  id(p1)
  a1i(p1, exist(s0,eq(c(s0),c(s1))))
  a6.2(s0, s1)
}
```

```follow
thm eq.exist.2(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))))
  diff (s0, s2) (s1, s2)
} = {
  syl(eq(c(s0),c(s1)), exist(s2,and(eq(c(s2),c(s0)),eq(c(s2),c(s1)))), eq(c(s0),c(s1)))
  iff.righti(exist(s2,and(eq(c(s2),c(s0)),eq(c(s2),c(s1)))), eq(c(s0),c(s1)))
  eq.exist.1(s2, s0, eq(c(s2),c(s1)), eq(c(s0),c(s1)))
  eq.trans(s2, s0, s1)
  id(eq(c(s0),c(s1)))
}
```

```follow
thm eq.exist.3(set s0, set s1, set s2) {
  |- imp(exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))), eq(c(s0), c(s1)))
  diff (s0, s2) (s1, s2)
} = {
  syl(exist(s2,and(eq(c(s2),c(s0)),eq(c(s2),c(s1)))), eq(c(s0),c(s1)), exist(s2,eq(c(s0),c(s1))))
  a5(s2, eq(c(s0),c(s1)))
  a4.aee.1i(s2, and(eq(c(s2),c(s0)),eq(c(s2),c(s1))), eq(c(s0),c(s1)))
  gen(s2, imp(and(eq(c(s2),c(s0)),eq(c(s2),c(s1))),eq(c(s0),c(s1))))
  and.impoi(eq(c(s2),c(s0)), eq(c(s2),c(s1)), eq(c(s0),c(s1)))
  eq.trans(s2, s0, s1)
}
```

```follow
thm eq.exist(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))))
  |- imp(exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))), eq(c(s0), c(s1)))
  |- iff(eq(c(s0), c(s1)), exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))))
  |- iff(exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))), eq(c(s0), c(s1)))
  diff (s0, s2) (s1, s2)
} = {
  iff.introii(eq(c(s0),c(s1)), exist(s2,and(eq(c(s2),c(s0)),eq(c(s2),c(s1)))))
  iff.introii(exist(s2,and(eq(c(s2),c(s0)),eq(c(s2),c(s1)))), eq(c(s0),c(s1)))
  eq.exist.2(s0, s1, s2)
  eq.exist.3(s0, s1, s2)
}
```

```follow
// 替换exist中使用的 s0 变量为 s1 变量
thm eq.subs.exist(prop p0, set s0, set s1) {
  |- imp(exist(s1, forall(s0, imp(eq(c(s0), c(s1)), p0))), exist(s0, p0))
  diff (s1, p0) (s1, s0)
} = {
  mp(imp(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))),exist(s0,p0)), forall(s1,imp(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s0,p0))))
  a4.diff(s1, forall(s0,imp(eq(c(s0),c(s1)),p0)), exist(s0,p0))
  gen(s1, imp(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s0,p0)))
  mp(imp(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s0,p0)), exist(s0,eq(c(s0),c(s1))))
  a6.1(s0, s1)
  com12i(exist(s0,eq(c(s0),c(s1))), forall(s0,imp(eq(c(s0),c(s1)),p0)), exist(s0,p0))
  a4(s0, eq(c(s0),c(s1)), p0)
}
```


a5.free

```follow
thm eq.forall.1(prop p0, set s0, set s1) {
  |- iff(forall(s0, imp(eq(c(s0), c(s1)), p0)), p0)
  |- imp(forall(s0, imp(eq(c(s0), c(s1)), p0)), p0)
  |- imp(p0, forall(s0, imp(eq(c(s0), c(s1)), p0)))
  diff (s0, s1) (s0, p0)
} = {
  iff.lefti(forall(s0,imp(eq(c(s0),c(s1)),p0)), p0)
  iff.righti(forall(s0,imp(eq(c(s0),c(s1)),p0)), p0)
  iff.mp(forall(s0,imp(eq(c(s0),c(s1)),p0)), p0, exist(s0,eq(c(s0),c(s1))))
  a6.1(s0, s1)
  a4.diff(s0, eq(c(s0),c(s1)), p0)
}
```

```follow
thm eq.forall.2(prop p0, set s0, prop p1, set s1) {
  |- imp(forall(s0, imp(eq(c(s0), c(s1)), p0)), p1)
  -| imp(eq(c(s0), c(s1)), imp(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  syl(forall(s0,imp(eq(c(s0),c(s1)),p0)), p1, forall(s0,imp(eq(c(s0),c(s1)),p1)))
  eq.forall.1(p1, s0, s1)
  a4.1i(s0, imp(eq(c(s0),c(s1)),p0), imp(eq(c(s0),c(s1)),p1))
  gen(s0, imp(imp(eq(c(s0),c(s1)),p0),imp(eq(c(s0),c(s1)),p1)))
  a2i(eq(c(s0),c(s1)), p0, p1)
}
```

```follow
thm eq.forall.3(prop p0, set s0, prop p1, set s1) {
  |- iff(forall(s0, imp(eq(c(s0), c(s1)), p0)), p1)
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  iff.syl(forall(s0,imp(eq(c(s0),c(s1)),p0)), p1, forall(s0,imp(eq(c(s0),c(s1)),p1)))
  eq.forall.1(p1, s0, s1)
  mp(iff(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(eq(c(s0),c(s1)),p1))), forall(s0,iff(imp(eq(c(s0),c(s1)),p0),imp(eq(c(s0),c(s1)),p1))))
  a4(s0, imp(eq(c(s0),c(s1)),p0), imp(eq(c(s0),c(s1)),p1))
  gen(s0, iff(imp(eq(c(s0),c(s1)),p0),imp(eq(c(s0),c(s1)),p1)))
  iff.a2i(eq(c(s0),c(s1)), p0, p1)
}
```

```follow
thm eq.forall.4(prop p0, set s0, prop p1, set s1) {
  |- imp(forall(s0, p0), forall(s1, p1))
  -| imp(eq(c(s0), c(s1)), imp(p0, p1)))
  diff (s0, s1) (s0, p1) (s1, p0)
} = {
  gend(s1, p1, forall(s0,p0))
  forall.special.2(s0, s1, p0, p1)
}
```
```follow
thm eq.forall.5(prop p0, set s0, prop p1, set s1) {
  |- iff(forall(s0, p0), forall(s1, p1))
  |- iff(forall(s1, p1), forall(s0, p0))
  |- imp(forall(s0, p0), forall(s1, p1))
  |- imp(forall(s1, p1), forall(s0, p0))
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1) (s1, p0)
} = {
  iff.comi(forall(s1,p1), forall(s0,p0))
  iff.lefti(forall(s0,p0), forall(s1,p1))
  iff.righti(forall(s0,p0), forall(s1,p1))
  iff.introii(forall(s0,p0), forall(s1,p1))
  eq.forall.4(p0, s0, p1, s1)
  eq.forall.4(p1, s1, p0, s0)
  syl(eq(c(s1),c(s0)), imp(p1,p0), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  iff.leftd(eq(c(s0),c(s1)), p0, p1)
  iff.rightd(eq(c(s0),c(s1)), p0, p1)
}
```


```follow
thm forall.special.5(set s0, prop p0, set s1, prop p1) {
  |- imp(forall(s0, p0), p0)
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  diff (s1, s0) (s1, p0) (s0, p1)
} = {
  syl(forall(s0,p0), p0, forall(s1,p1))
  forall.special.1(s1, s0, p1, p0)
  a5(s1, not(p0))
  syl(eq(c(s1),c(s0)), imp(p1,p0), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  iff.rightd(eq(c(s0),c(s1)), p0, p1)
  eq.forall.5(p0, s0, p1, s1)
}
```

## `a6` 

```follow
thm a6(set s0, set s1) {
  |- not(forall(s0, not(eq(c(s0), c(s1)))))
  |- exist(s0, eq(c(s0), c(s1)))
  |- not(forall(s0, not(eq(c(s1), c(s0)))))
  |- exist(s0, eq(c(s1), c(s0)))
  diff (s0, s1)
} = {
  mp(not(forall(s0,not(eq(c(s1),c(s0))))), not(forall(s0, not(eq(c(s0), c(s1))))))
  con3i(forall(s0,not(eq(c(s0),c(s1)))), forall(s0,not(eq(c(s1),c(s0)))))
  a4.1i(s0, not(eq(c(s1),c(s0))), not(eq(c(s0),c(s1))))
  gen(s0, imp(not(eq(c(s1),c(s0))),not(eq(c(s0),c(s1)))))
  con3i(eq(c(s1),c(s0)), eq(c(s0),c(s1)))
  mp(exist(s0,eq(c(s1),c(s0))), exist(s0, eq(c(s0), c(s1))))
  mp(imp(exist(s0,eq(c(s0),c(s1))),exist(s0,eq(c(s1),c(s0)))), forall(s0,imp(eq(c(s0),c(s1)),eq(c(s1),c(s0)))))
  a4(s0, eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  gen(s0, imp(eq(c(s0),c(s1)),eq(c(s1),c(s0))))
  eq.com(s0, s1)
  a6.1(s0, s1)
}
```